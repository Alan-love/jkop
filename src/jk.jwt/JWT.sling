
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.sha
import jk.json
import jk.time
import jk.rs256
import jk.base64

class:

func encodeUrlSafe(buff as buffer) static as string:
	return Base64Encoder.toUrlSafeFormat(Base64Encoder.encode(buff))

func decode(token as string, keys as map?) static as this
{
	var comps = String.split(token, '.', 3)
	if Vector.getSize(comps) != 3:
		Error.throw("invalidJwtTokenFormat", token)
	var headerBuffer = Base64Decoder.decode(Base64Decoder.fromUrlSafeFormat(Vector.getValue(comps, 0) ?? ""))
	var headerJson = String.forUTF8Buffer(headerBuffer)
	var header = JSONParser.parseStringAsMap(headerJson)
	var claimsBuffer = Base64Decoder.decode(Base64Decoder.fromUrlSafeFormat(Vector.getValue(comps, 1) ?? ""))
	var claimsJson = String.forUTF8Buffer(claimsBuffer)
	var claims = JSONParser.parseStringAsMap(claimsJson)
	var signature = Base64Decoder.decode(Base64Decoder.fromUrlSafeFormat(Vector.getValue(comps, 2) ?? ""))
	var alg = header.getString("alg")
	if String.isEmpty(alg) || String.equalsIgnoreCase(alg, "none") {
		nop // token was not signed, so no need to check.
	}
	else if keys {
		if String.equalsIgnoreCase(alg, "HS256") {
			var key = Buffer.asValueOrThrowError(Map.getValue(keys, "hs256"))
			var data = Vector.getValue(comps, 0) .. "." .. Vector.getValue(comps, 1)
			var sigdata = encodeUrlSafe(HmacSha256.encode(String.toUTF8Buffer(data), key))
			if sigdata != Vector.getValue(comps, 2):
				Error.throw("invalidJwtSignature", token)
		}
		else if String.equalsIgnoreCase(alg, "RS256") {
			var rsa = RS256.instance()
			var key = String.asValueOrThrowError(Map.getValue(keys, "rs256"))
			var data = Vector.getValue(comps, 0) .. "." .. Vector.getValue(comps, 1)
			if not rsa.validate(String.toUTF8Buffer(data), signature, key):
				Error.throw("invalidJwtSignature", token)
		}
		else {
			Error.throw("unsupportedJwtAlgorithm", alg)
		}
	}
	else {
		nop // explicitly requested NOT to check the signature
	}
	return new this() {
		claims claims
	}
}

prop claims as DynamicMap

func setIssuer(v as string?):
	claims.setString("iss", v)

func getIssuer as string?:
	return claims.getString("iss")

func setSubject(v as string?):
	claims.setString("sub", v)

func getSubject as string?:
	return claims.getString("sub")

func setAudience(v as string?):
	claims.setString("aud", v)

func getAudience as string?:
	return claims.getString("aud")

func setExpirationTime(v as long):
	claims.setString("exp", String.forLongInteger(v))

func getExpirationTime as long:
	return claims.getLongIntegerOrDefaultValue("exp", 0)

func setNotBefore(v as long):
	claims.setString("nbf", String.forLongInteger(v))

func getNotBefore as long:
	return claims.getLongIntegerOrDefaultValue("nbf", 0)

func setIssuedAt(v as long):
	claims.setString("iat", String.forLongInteger(v))

func getIssuedAt as long:
	return claims.getLongIntegerOrDefaultValue("iat", 0)

func setJwtId(v as string?):
	claims.setString("jti", v)

func getJwtId as string?:
	return claims.getString("jti")

func setClaim(key as string, value as string):
	claims.setString(key, value)

func getClaim(key as string) as string?:
	return claims.getString(key)

func validate(issuer as string?, audience as string?) as bool
{
	assert isActive()
	if issuer:
		assert isIssuer(issuer)
	if audience:
		assert isAudience(audience)
	return true
}

func isIssuer(value as string) as bool:
	return String.equals(claims.getString("iss"), value)

func isAudience(value as string) as bool:
	return String.equals(claims.getString("aud"), value)

func isActive as bool
{
	assert isExpired() == false
	var nbf = claims.getLongIntegerOrDefaultValue("nbf", 0)
	if nbf < 1:
		return true
	if SystemClock.asUTCSeconds() < nbf:
		return false
	return true
}

func isExpired private as bool
{
	var exp = claims.getLongIntegerOrDefaultValue("exp", 0)
	if exp < 1:
		return false
	if SystemClock.asUTCSeconds() < exp:
		return false
	return true
}

func generateHeaderContent(alg as string) as string
{
	var v = new DynamicMap()
	v.setString("alg", alg)
	v.setString("typ", "JWT")
	return JSONEncoder.toString(v)
}

func generateClaimsContent as string:
	return JSONEncoder.toString(claims)

func generateTokenWithRS256(privateKey as string) as string
{
	var rsa = RS256.instance()
	var sb = new StringBuilder()
	sb.appendString(encodeUrlSafe(String.toUTF8Buffer(generateHeaderContent("RS256"))))
	sb.appendCharacter('.')
	sb.appendString(encodeUrlSafe(String.toUTF8Buffer(generateClaimsContent())))
	var data = sb.toString()
	var sigdata = rsa.generate(String.toUTF8Buffer(data), privateKey)
	var sb2 = new StringBuilder()
	sb2.appendString(data)
	sb2.appendCharacter('.')
	sb2.appendString(encodeUrlSafe(sigdata))
	return sb2.toString()
}

func generateTokenWithHS256(key as buffer) as string
{
	var sb = new StringBuilder()
	sb.appendString(encodeUrlSafe(String.toUTF8Buffer(generateHeaderContent("HS256"))))
	sb.appendCharacter('.')
	sb.appendString(encodeUrlSafe(String.toUTF8Buffer(generateClaimsContent())))
	var data = sb.toString()
	var sigdata = HmacSha256.encode(String.toUTF8Buffer(data), key)
	var sb2 = new StringBuilder()
	sb2.appendString(data)
	sb2.appendCharacter('.')
	sb2.appendString(encodeUrlSafe(sigdata))
	return sb2.toString()
}
