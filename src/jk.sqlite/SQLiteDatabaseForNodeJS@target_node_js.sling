
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.fs
import jk.sql
import jk.log
import jk.lang.js

class is SQLiteDatabase:

depend "npm:better-sqlite3:8.4.0"

var sqldatabase as [magical:sqlite.Database]?

class Statement is SQLStatement
{
	func create(db as [magical:sqlite.Database], statement as string) static throws Exception as this
	{
		var sqlPreparedStmt = db.prepare!(statement)
		if sqlPreparedStmt == null:
			Error.throw("failedToPrepareSqlStatement", statement)
		return new this() {
			sqlitedb db
			sqlPreparedStmt sqlPreparedStmt as! [magical:sqlite.Statement]
		}
	}

	prop sqlitedb as [magical:sqlite.Database]
	prop sqlPreparedStmt as [magical:sqlite.Statement]

	var params = new vector<dynamic>

	func addParamString(val as string) override:
		params += val

	func addParamInteger(val as int) override:
		params += val

	func addParamLongInteger(val as long) override:
		params += val

	func addParamDouble(val as double) override:
		params += val

	func addParamBlob(val as buffer) override:
		params += val

	func setParamString(name as string, val as string) override:
		addParamString(val)

	func setParamInteger(name as string, val as int) override:
		addParamInteger(val)

	func setParamLongInteger(name as string, val as long) override:
		addParamLongInteger(val)

	func setParamDouble(name as string, val as double) override:
		addParamDouble(val)

	func setParamBlob(name as string, val as buffer) override:
		addParamBlob(val)

	func resetStatement override throws Exception:
		Vector.clear(params)

	func executeUpdateDelete throws Exception as int
	{
		var result = sqlPreparedStmt.run!(params)
		if result == null:
			Error.throw("failedToExecuteSqlStatement", this)
		return Integer.asValueOrDefaultValue(result.changes, 0)
	}

	func execute:
		sqlPreparedStmt.run!(params)

	func getIteratorStmt throws Exception as dynamic
	{
		var v = sqlPreparedStmt.bind!(params)
		if v == null:
			Error.throw("failedToBindParameters", sqlPreparedStmt)
		return v
	}
}

class ResultSet is SQLResultSetIterator
{
	var nextRowValue as vector<dynamic?>?
	var currentRowValue as vector<dynamic?>?
	var rawColumnData as vector<dynamic?>?
	var columnNames = new vector<string>
	var iterator as [magical:sqlite.Iterator]?

	ctor(stmt as Statement) throws Exception
	{
		var istmt = stmt.getIteratorStmt()
		rawColumnData = istmt.columns!() as vector<dynamic?>
		iterator = istmt.raw!()!.iterate!() as! [magical:sqlite.Iterator]?
	}

	func hasNext override as bool
	{
		if nextRowValue:
			return true
		if not iterator:
			return false
		var itr = iterator.next!()
		if itr == null:
			return false
		if Boolean.asValueOrDefaultValue(itr.done) == true:
			return false
		nextRowValue = itr.value as vector<dynamic?>
		return true
	}

	func step override throws Exception
	{
		if not hasNext():
			Error.throw("noNextRecord", this)
		currentRowValue = nextRowValue
		nextRowValue = null
	}

	func next override throws Exception as DynamicMap
	{
		step()
		var columns = getColumnNames()
		var data = new DynamicMap()
		for(var i = 0 ; i < Vector.getSize(columns) ; i++):
			data.setDynamic(columns[i], getColumnValue(i))
		return data
	}

	func nextValues(values as vector<dynamic?>) override throws Exception
	{
		step()
		Vector.clear(values)
		var columns = getColumnCount()
		for(var i = 0 ; i < columns ; i++):
			values += getColumnValue(i)
	}

	func getColumnInfo(n as int) private throws Exception as dynamic
	{
		if not rawColumnData:
			Error.throw("dataNotInitialized", "rawColumnData")
		var v = Vector.getValue(rawColumnData, n)
		if not v:
			Error.throw("invalidColumnIndex", String.forInteger(n))
		return v
	}

	func getColumnName(n as int) override throws Exception as string:
		return String.asValueOrThrowError(getColumnInfo(n).name)

	func getColumnCount override throws Exception as int
	{
		if rawColumnData == null:
			return 0
		return Vector.getSize(rawColumnData)
	}

	func getColumnNames override throws Exception as vector<string>
	{
		if Vector.getSize(columnNames) > 0:
			return columnNames
		if rawColumnData == null:
			return columnNames
		foreach next in Vector.iterate(rawColumnData) {
			if next == null:
				Error.throw("nullEntryInRawColumnData")
			columnNames += String.asValueOrThrowError(next.name)
		}
		return columnNames
	}

	/*[
	func getColumnType(n as int) private throws Exception as string
	{
		var info = getColumnInfo(n)
		var type = String.asValueOrNull(info.type)
		if type == null && String.asValueOrNull(info.name) != null:
			type = "TEXT"
		if not type:
			Error.throw("unableToDetermineColumnType", info)
		return type
	}
	]*/

	func getColumnValue(n as int) override throws Exception as dynamic?
	{
		var values = currentRowValue
		if not values:
			Error.throw("noCurrentRow")
		if n < 0 || n >= Vector.getSize(values):
			Error.throw("invalidColumnIndex", String.forInteger(n))
		return values[n]
	}

	func getColumnInt(n as int) override throws Exception as int:
		return Integer.asValueOrThrowError(getColumnValue(n))

	func getColumnDouble(n as int) override throws Exception as double:
		return Double.asValueOrThrowError(getColumnValue(n))

	func getColumnBuffer(n as int) override throws Exception as buffer:
		return Buffer.asValueOrThrowError(getColumnValue(n))

	func getColumnString(n as int) throws Exception as string:
		return String.asValueOrThrowError(getColumnValue(n))

	func getColumnLong(n as int) override throws Exception as long:
		return LongInteger.asValueOrThrowError(getColumnValue(n))

	func close override throws Exception
	{
		rawColumnData = null
		columnNames = new vector<string>
		iterator = null
		currentRowValue = null
		nextRowValue = null
	}
}

func initialize(file as File, openCreate as bool) override throws Exception
{
	var sqlite = JsCommonModule.require("better-sqlite3")
	var ctx as LoggingContext?
	if openCreate == false && file.isFile() == false:
		Error.throw("databaseDoesNotExist", file)
	sqldatabase = sqlite(file.getPath()) as! [magical:sqlite.Database]
	Log.debug(ctx, "Opened Sqlite database: '" .. file.getPath() .. "'")
}

func closeConnection async override throws Exception
{
	lang "js" {{{
		if (this.sqldatabase !== null) {
			this.sqldatabase.close();
			this.sqldatabase = null;
		}
	}}}
}

func prepareStatement(sql as string) async override throws Exception as SQLStatement
{
	if not sqldatabase:
		Error.throw("databaseNotInitialized", sqldatabase)
	return Statement.create(sqldatabase, sql)
}

func executeStatement(stmt as SQLStatement) async override throws Exception
{
	var ss = stmt as Statement
	if not ss:
		Error.throw("invalidSqliteStatementObject", stmt)
	ss.execute()
}

func executeQueryStatement(stmt as SQLStatement) async override throws Exception as SQLResultSetIterator
{
	var ss = stmt as Statement
	if not ss:
		Error.throw("invalidSqliteStatementObject", stmt)
	return new ResultSet(ss)
}

func executeUpdateDeleteStatement(stmt as SQLStatement) async override throws Exception as int
{
	var ss = stmt as Statement
	if not ss:
		Error.throw("invalidSqliteStatementObject", stmt)
	return ss.executeUpdateDelete()
}
