
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.io

class is File private:

var fs = lang "js" as dynamic {{{ require("fs") }}}

class MyFileReader is FileReader is SizedReader is Reader is Closable is SeekableReader
{
	prop file as File
	prop fd as int
	prop fs as dynamic

	var position = 0

	func read(buffer as buffer) implement throws Exception as int
	{
		var sz = Buffer.getSize(buffer)
		var fd = this.fd
		var fs = this.fs
		var v = lang "js" (fd, fs) as int {{{ fs.readSync(fd, buffer, 0, sz, this.position) }}}
		position += sz
		return v
	}

	func getSize implement as long:
		return file.getSize()

	func close implement
	{
		var fd = this.fd
		if fd >= 0 {
			var fs = this.fs
			lang "js" (fs) {{{
				fs.closeSync(fd);
			}}}
			this.fd = -1
		}
	}

	func setCurrentPosition(n as int64) implement as bool
	{
		this.position = n as! int
		return true
	}

	func getCurrentPosition implement as int64:
		return this.position as! int64
}

class MyFileWriter is FileWriter
{
	prop fd as int
	prop fs as dynamic

	func write(buf as buffer, size as int) implement throws Exception as int
	{
		var sz = size
		if sz < 0:
			sz = sizeof buf
		var fd = this.fd
		var fs = this.fs
		var v = lang "js" (fd, fs) as int {{{ fs.writeSync(fd, Buffer.from(buf), 0, sz, null) }}}
		return v
	}

	func close implement
	{
		var fd = this.fd
		if fd >= 0 {
			var fs = this.fs
			lang "js" (fs) {{{
				fs.closeSync(fd);
			}}}
			this.fd = -1
		}
	}
}

var path as string?

func forPath(path as string) static as File
{
	var v = new this()
	v.setPath(path)
	return v
}

func entry(name as string) override as File
{
	var path = getPath()
	var pp = lang "js" (path, name) as string {{{ require("path").join(path, name); }}}
	return forPath(pp)
}

func move(dest as File, replace as bool) override throws Exception
{
	if dest.exists() {
		if replace == false:
			Error.throw("destinationFileExists", dest)
		dest.remove()
	}
	var path = getPath()
	lang "js" (path) {{{
		this.fs.renameSync(path, dest.path);
	}}}
}

func touch override throws Exception
{
	var path = getPath()
	var fs = this.fs
	lang "js" (path, fs) {{{
		fs.closeSync(fs.openSync(path, 'w'));
	}}}
}

func read override throws Exception as FileReader
{
	var path = getPath()
	var fd = lang "js" (path) as int {{{ this.fs.openSync(path, "r"); }}}
	if fd < 0:
		Error.throw("failedToOpenFile", this)
	return new MyFileReader() {
		file this
		fd fd
		fs fs
	}
}

func write override throws Exception as FileWriter
{
	var path = getPath()
	var fd = lang "js" (path) as int {{{ this.fs.openSync(path, "w"); }}}
	if fd < 0:
		Error.throw("failedToOpenFile", this)
	return new MyFileWriter() {
		fd fd
		fs fs
	}
}

func append override throws Exception as FileWriter
{
	var path = getPath()
	var fd = lang "js" (path) as int {{{ this.fs.openSync(path, "a"); }}}
	if fd < 0:
		Error.throw("failedToOpenFile", this)
	return new MyFileWriter() {
		fd fd
		fs fs
	}
}

func stat override as FileInfo
{
	var path = getPath()
	var v = new FileInfo() {
		file this
		type FileInfo.FILE_TYPE_UNKNOWN
	}
	var lss as dynamic?
	try {
		lss = lang "js" (path) as dynamic {{{ this.fs.lstatSync(path) }}}
	}
	catch {
		lss = null
	}
	if lss {
		var ss = lss
		if lang "js" as bool {{{ lss.isSymbolicLink() }}} {
			v.link = true
			try {
				ss = lang "js" (path) as dynamic {{{ this.fs.statSync(path) }}}
			}
			catch {
				ss = lss
			}
		}
		v.exists = true
		v.size = lang "js" as long {{{ ss.size }}}
		v.createTime = lang "js" as long {{{ ~~(ss.ctimeMs / 1000) }}}
		v.accessTime = lang "js" as long {{{ ~~(ss.atimeMs / 1000) }}}
		v.modifyTime = lang "js" as long {{{ ~~(ss.mtimeMs / 1000) }}}
		v.ownerUser = lang "js" as int {{{ ss.uid }}}
		v.ownerGroup = lang "js" as int {{{ ss.gid }}}
		v.mode = lang "js" as int {{{ ss.mode }}}
		v.executable = false
		if lang "js" as bool {{{ ss.isFile() }}} {
			v.type = FileInfo.FILE_TYPE_FILE
		}
		else if lang "js" as bool {{{ ss.isDirectory() }}} {
			v.type = FileInfo.FILE_TYPE_DIR
		}
		if lang "js" as bool {{{ ss.isSymbolicLink() }}}:
			v.link = true
	}
	else {
		v.exists = false
	}
	return v
}

func isExecutable override as bool
{
	var path = getPath()
	var rr as bool
	lang "js" (path) {{{
		try {
			this.fs.accessSync(path, this.fs.constants.X_OK);
			rr = true;
		}
		catch (err) {
			rr = false;
		}
	}}}
	return rr
}

func createDirectory override throws Exception
{
	var path = getPath()
	lang "js" (path) {{{
		this.fs.mkdirSync(path);
	}}}
}

func createDirectoryRecursive override throws Exception
{
	var path = getPath()
	lang "js" (path) {{{
		this.fs.mkdirSync(path, {recursive: true});
	}}}
}

func removeDirectory override throws Exception
{
	if exists() {
		var path = getPath()
		lang "js" (path) {{{
			this.fs.rmdirSync(path);
		}}}
	}
}

func setPath(newPath as string)
{
	var pp = lang "js" as dynamic {{{ require("path") }}}
	if pp.isAbsolute!(newPath) as! bool == true {
		lang "js" {{{
			this.path = pp.normalize(newPath);
		}}}
	}
	else {
		var cwd = CurrentDirectory.get().getPath()
		lang "js" (cwd, newPath) {{{
			this.path = pp.resolve(cwd, newPath);
		}}}
	}
}

func getPath override as string
{
	if not path:
		return ""
	return path
}

func remove override throws Exception
{
	var path = getPath()
	lang "js" (path) {{{
		this.fs.rmSync(path);
	}}}
}

func makeExecutable override throws Exception
{
	var path = getPath()
	lang "js" (path) {{{
		this.fs.openSync(path, this.fs.constants.S_IXUSR);
	}}}
}

func entries override throws Exception as Iterator<File>
{
	var path = getPath()
	var v = new vector<File>
	if exists() {
		lang "js" (path) {{{
			const dir = this.fs.opendirSync(path);
			let dirent;
			while ((dirent = dir.readSync()) !== null) {
				v.push(FileForNodeJS.forPath(path + "/" + dirent.name));
			}
			dir.closeSync();
		}}}
	}
	return Vector.iterate(v)
}

func setMode(mode as int) override throws Exception
{
	var path = getPath()
	lang "js" (path, mode) {{{
		this.fs.chmodSync(path, mode);
	}}}
}

func setOwnerUser(uid as int) override throws Exception
{
	var path = getPath()
	var st = stat()
	lang "js" (path, st, uid) {{{
		this.fs.chownSync(path, uid, st.getOwnerGroup());
	}}}
}

func setOwnerGroup(gid as int) override throws Exception
{
	var path = getPath()
	var st = stat()
	lang "js" (path, st, gid) {{{
		this.fs.chownSync(path, st.getOwnerUser(), gid);
	}}}
}

func createFifo override throws Exception:
	ERROR NOT_SUPPORTED
