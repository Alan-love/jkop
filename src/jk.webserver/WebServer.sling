
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.log

class abstract:

prop ctx as LoggingContext
prop errorHandler as function<(WebServerRequest,dynamic) async throws Exception>?

var middlewares = new vector<WebServerMiddleware>

func instance(ctx as LoggingContext) static throws Exception as this
{
	IFDEF "target_node_js" {
		var v = new WebServerForNodeJS() {
			ctx ctx
		}
		v.initialize()
		return v
	}
	ELSE IFDEF "target_gui" {
		nop ctx
		ERROR NOT_SUPPORTED
	}
	ELSE {
		nop ctx
		ERROR NOT_IMPLEMENTED
	}
}

func waitForServers(ctx as LoggingContext, servers as vector<this>) async static throws Exception
{
	IFDEF "target_node_js" {
		await WebServerForNodeJS.waitForServers(ctx, servers)
	}
	ELSE IFDEF "target_gui" {
		nop ctx
		nop servers
		ERROR NOT_SUPPORTED
	}
	ELSE {
		nop ctx
		nop servers
		ERROR NOT_IMPLEMENTED
	}
}

func initialize virtual throws Exception:
	nop

func useFunction(function as function<(WebServerRequest,function<() async throws Exception>) async>):
	use(new WebServerMiddleware() {
		handler function
	})

func asHttpErrorCode(error as dynamic?) as int
{
	if error == null:
		return 500
	var wse = error as WebServerException
	if wse == null:
		return 500
	return wse.httpStatus
}

func handleError(req as WebServerRequest, error as dynamic) async virtual
{
	try {
		var handler = errorHandler
		if handler {
			await handler(req, error)
			return
		}
	}
	Log.error(ctx, String.asValueOrEmptyString(error))
	try {
		req.setResponseStatus(asHttpErrorCode(error))
		await req.endResponse()
	}
}

class HandleRequestState private
{
	prop index as int = 0
}

func handleRequest(req as WebServerRequest) async virtual
{
	var state = new HandleRequestState()
	var nextf = func async throws Exception {
	}
	nextf = func async throws Exception {
		var mw = Vector.getValue(middlewares, state.index)
		if mw {
			state.index = state.index + 1
			await mw.handleRequest(req, nextf)
		}
		else {
			throw WebServerException.forNotFound()
		}
	}
	try {
		await nextf()
	}
	catch e {
		await handleError(req, e)
	}
}

func use(middleware as WebServerMiddleware) virtual
{
	middleware.server = this
	middlewares += middleware
	middleware.initializeMiddleware(this)
}

func start(port as int) async abstract throws Exception
func execute(port as int) async abstract throws Exception
func stop abstract throws Exception
