
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.log

class abstract:

description [[
	SQL database connection. This is the base class and abstraction for all
	database-specific driver classes.
]]

prop ctx as LoggingContext

func closeConnection async abstract
func getDatabaseTypeId abstract as string
func getLastInsertId(table as string) abstract async as long
func getIdentityColumnName(table as string) abstract async as string?
func getPrimaryKeyColumnName(table as string) abstract async as string?
func prepareStatement(sql as string) async abstract as SQLStatement
func executeStatement(stmt as SQLStatement) abstract async
func executeUpdateDeleteStatement(stmt as SQLStatement) abstract async as int
func executeQueryStatement(stmt as SQLStatement) abstract async as SQLResultSetIterator
func checkIfTableExists(table as string) abstract async as bool
func checkIfColumnExists(table as string, name as string) abstract async as bool
func checkIfColumnTypeMatches(table as string, column as SQLTableColumnInfo) abstract async as bool
func getAllTableNames abstract async as vector<string>
func prepareCreateTableStatement(table as string, columns as vector<SQLTableColumnInfo>) async abstract as SQLStatement
func prepareDeleteTableStatement(table as string) async abstract as SQLStatement
func prepareCreateColumnStatement(table as string, column as SQLTableColumnInfo) async abstract as SQLStatement
func prepareUpdateColumnTypeStatement(table as string, column as SQLTableColumnInfo) async abstract as SQLStatement

func getSingleRowOrNull(stmt as SQLStatement) virtual async as DynamicMap?
{
	var it = await executeQueryStatement(stmt)
	if not it.hasNext():
		return null
	var v = it.next()
	it.close()
	return v
}

func getSingleRowOrThrowError(stmt as SQLStatement) virtual async as DynamicMap
{
	var it = await executeQueryStatement(stmt)
	if not it.hasNext():
		ERROR "notFound"
	var v = it.next()
	it.close()
	return v
}

func ensureColumnsExistInTable(tableName as string, columns as vector<SQLTableColumnInfo>) async
{
	description [[
		Checks if the database table exists and creates it if it does not exist. Returns
		false if something went wrong.
	]]
	foreach column in columns {
		if await checkIfColumnExists(tableName, column.name) {
			if not await checkIfColumnTypeMatches(tableName, column):
				await executeStatement(await prepareUpdateColumnTypeStatement(tableName, column))
		}
		else {
			await executeStatement(await prepareCreateColumnStatement(tableName, column))
		}
	}
}

func ensureTableExists(table as SQLTableInfo) virtual async
{
	description [[
		Checks if the database table exists and creates it if it does not exist. Throws
		an exception in any failure scenario.
	]]
	var name = table.name
	if String.isEmpty(name):
		Error.throw("emptyTableName", table)
	if await checkIfTableExists(name) {
		await ensureColumnsExistInTable(name, table.columns)
		return
	}
	await executeInTransaction(func async {
		await executeStatement(await prepareCreateTableStatement(name, table.columns))
		foreach cii in table.indexes:
			await executeStatement(await prepareCreateIndexStatement(name, cii.columns, cii.unique))
	})
}

func prepareCreateIndexStatement(table as string, columns as vector<string>, unique as bool) async virtual as SQLStatement
{
	description [[
		Prepares a CREATE INDEX statement as a string for the supplied table name,
		column name and a boolean flag which indicates if the index will be unique.
	]]
	if Vector.getSize(columns) < 1:
		Error.throw("noColumnsForPrepareIndexStatement", table)
	var namesb = new StringBuilder()
	namesb.appendString(table)
	foreach column in columns {
		namesb.appendCharacter('_')
		namesb.appendString(column)
	}
	var idxname = prepareColumnName(namesb.toString())
	var unq = ""
	if unique:
		unq = "UNIQUE "
	var sqlsb = new StringBuilder()
	sqlsb.appendString("CREATE ")
	sqlsb.appendString(unq)
	sqlsb.appendString("INDEX ")
	sqlsb.appendString(idxname)
	sqlsb.appendString(" ON ")
	sqlsb.appendString(prepareTableName(table))
	sqlsb.appendString(" (")
	var first = true
	foreach column in columns {
		if first {
			sqlsb.appendString(" ")
		}
		else {
			sqlsb.appendString(", ")
		}
		first = false
		sqlsb.appendString(prepareColumnName(column))
	}
	sqlsb.appendString(" )")
	return await prepareStatement(sqlsb.toString())
}

func createColumnSelectionString(columns as vector<string>?) private as string
{
	description [[
		Creates the column selection part of an SQL select statement in string from the
		supplied array of column name(s).
	]]
	if not columns || sizeof columns < 1:
		return "*"
	var sb = new StringBuilder()
	var first = true
	foreach column in columns {
		if not first:
			sb.appendString(", ")
		sb.appendString(prepareColumnName(column))
		first = false
	}
	return sb.toString()
}

func createOrderByString(order as vector<SQLOrderingRule>) private as string
{
	description [[
		Creates the order by part of an SQL select statement in string from the supplied
		array of column name(s) or instance(s) of SQLOrderingRule class.
	]]
	if sizeof order < 1:
		Error.throw("emptyOrderByRuleVector", order)
	var sb = new StringBuilder()
	sb.appendString(" ORDER BY ")
	var first = true
	foreach sr in order {
		if not first:
			sb.appendString(", ")
		sb.appendString(prepareColumnName(sr.column))
		sb.appendCharacter(' ')
		if sr.descending {
			sb.appendString("DESC")
		}
		else {
			sb.appendString("ASC")
		}
		first = false
	}
	return sb.toString()
}

func prepareSelectAllStatement(table as string, columns as vector<string>? = null, order as vector<SQLOrderingRule>? = null) async virtual as SQLStatement
{
	description [[
		Creates an instance of SQLStatement for select statement with the specified
		database table name, an array of column name(s) and an array of column name(s)
		or instance(s) of SQLOrderingRule for order by clause.
	]]
	var sb = new StringBuilder()
	sb.appendString("SELECT ")
	sb.appendString(createColumnSelectionString(columns))
	sb.appendString(" FROM ")
	sb.appendString(prepareTableName(table))
	if order:
		sb.appendString(createOrderByString(order))
	sb.appendString(";")
	return await prepareStatement(sb.toString())
}

func prepareSelectWithCriteriaStatement(table as string, criteria as DynamicMap?, limit as int = 0, offset as int = 0, columns as vector<string>? = null, order as vector<SQLOrderingRule>? = null, likeOperator as bool = false) async virtual as SQLStatement
{
	description [[
		Creates an instance of SQLStatement for select statement with the specified
		database table name, a map of criteria, limit, offset, an array of column
		name(s), an array of column name(s) or instance(s) of SQLOrderingRule for order
		by clause and a flag to use LIKE operator.
	]]
	var sb = new StringBuilder()
	sb.appendString("SELECT ")
	sb.appendString(createColumnSelectionString(columns))
	sb.appendString(" FROM ")
	sb.appendString(prepareTableName(table))
	var first = true
	var keys as vector<string>?
	if criteria {
		keys = criteria.getKeys()
		foreach key in keys {
			if first {
				sb.appendString(" WHERE ")
				first = false
			}
			else {
				sb.appendString(" AND ")
			}
			sb.appendString(prepareColumnName(key))
			var value = criteria.getString(key)
			if value == null {
				sb.appendString(" IS NULL")
			}
			else if likeOperator {
				sb.appendString(" LIKE ?")
			}
			else {
				sb.appendString(" = ?")
			}
		}
	}
	if order:
		sb.appendString(createOrderByString(order))
	if limit > 0 {
		sb.appendString(" LIMIT ")
		sb.appendString(String.forInteger(limit))
	}
	if offset > 0 {
		sb.appendString(" OFFSET ")
		sb.appendString(String.forInteger(offset))
	}
	sb.appendCharacter(';')
	var sql = sb.toString()
	var stmt = await prepareStatement(sql)
	if keys && criteria: foreach key in keys {
		var val = criteria.getString(key)
		if val == null:
			continue
		if likeOperator {
			stmt.addParamString("%" .. val .. "%")
		}
		else {
			stmt.addParamString(val)
		}
	}
	return stmt
}

func prepareCountRecordsStatement(table as string, criteria as DynamicMap?, likeOperator as bool = false) async virtual as SQLStatement
{
	description [[
		Creates an instance of SQLStatement for select count statement with the
		specified database table name and a map of criteria.
	]]
	var sb = new StringBuilder()
	sb.appendString("SELECT COUNT(*) AS count FROM ")
	sb.appendString(prepareTableName(table))
	var first = true
	var keys as vector<string>?
	if criteria {
		keys = criteria.getKeys()
		foreach key in keys {
			if first {
				sb.appendString(" WHERE ")
				first = false
			}
			else {
				sb.appendString(" AND ")
			}
			sb.appendString(prepareColumnName(key))
			var value = criteria.getString(key)
			if value == null {
				sb.appendString(" IS NULL")
			}
			else if likeOperator {
				sb.appendString(" LIKE ?")
			}
			else {
				sb.appendString(" = ?")
			}
		}
	}
	sb.appendCharacter(';')
	var stmt = await prepareStatement(sb.toString())
	if keys && criteria: foreach key in keys {
		var val = criteria.getString(key)
		if val == null:
			continue
		if likeOperator {
			stmt.addParamString("%" .. val .. "%")
		}
		else {
			stmt.addParamString(val)
		}
	}
	return stmt
}

func prepareSelectDistinctValuesStatement(table as string, column as string) async virtual as SQLStatement
{
	description [[
		Creates an instance of SQLStatement for query distinct values with the specified
		database table name and column name.
	]]
	var sb = new StringBuilder()
	sb.appendString("SELECT DISTINCT ")
	sb.appendString(prepareColumnName(column))
	sb.appendString(" FROM ")
	sb.appendString(prepareTableName(table))
	sb.appendString(";")
	return await prepareStatement(sb.toString())
}

func prepareInsertStatement(table as string, data as DynamicMap) async virtual as SQLStatement
{
	description [[
		Creates an instance of SQLStatement for insert statement with the specified
		database table name and an instance of DynamicMap for values.
	]]
	var sb = new StringBuilder()
	sb.appendString("INSERT INTO ")
	sb.appendString(prepareTableName(table))
	sb.appendString(" ( ")
	var first = true
	var keys = data.getKeys()
	foreach key in keys {
		if not first:
			sb.appendCharacter(',')
		sb.appendString(prepareColumnName(key))
		first = false
	}
	sb.appendString(" ) VALUES ( ")
	first = true
	foreach key in keys {
		if not first:
			sb.appendCharacter(',')
		if data.get(key) == null {
			sb.appendString("NULL")
		}
		else {
			sb.appendCharacter('?')
		}
		first = false
	}
	sb.appendString(" );")
	var stmt = await prepareStatement(sb.toString())
	foreach key in keys {
		var o = data.get(key)
		if o == null:
			continue
		if o is string {
			stmt.addParamString(o as! string)
		}
		else if o is buffer {
			stmt.addParamBlob(o as! buffer)
		}
		else if o is StringObject {
			stmt.addParamString((o as! StringObject).toString())
		}
		else if o is IntegerObject {
			stmt.addParamInteger((o as! IntegerObject).toInteger())
		}
		else if o is LongIntegerObject {
			stmt.addParamLongInteger((o as! LongIntegerObject).toLongInteger())
		}
		else if o is DoubleObject {
			stmt.addParamDouble((o as! DoubleObject).toDouble())
		}
		else if o is BufferObject {
			stmt.addParamBlob((o as! BufferObject).toBuffer())
		}
		else if o is BooleanObject {
			stmt.addParamInteger(((o as! BooleanObject).toBoolean()) ? 1 : 0)
		}
		else {
			var s = String.asValueOrNull(o)
			if not s:
				Error.throw("unsupportedDataRecordDataValue", o)
			stmt.addParamString(s)
		}
	}
	return stmt
}

func prepareUpdateStatement(table as string, criteria as DynamicMap?, data as DynamicMap) async virtual as SQLStatement
{
	description [[
		Creates an instance of SQLStatement for update statement with the specified
		database table name, an instance of DynamicMap for criteria and an instance of
		DynamicMap for values.
	]]
	var sb = new StringBuilder()
	sb.appendString("UPDATE ")
	sb.appendString(prepareTableName(table))
	sb.appendString(" SET ")
	var params = new vector<dynamic>
	var first = true
	foreach key in data.iterateKeys() {
		if not first:
			sb.appendString(", ")
		sb.appendString(prepareColumnName(key))
		var value = data.get(key)
		if value == null {
			sb.appendString(" = NULL")
		}
		else {
			sb.appendString(" = ?")
			params += value
		}
		first = false
	}
	if criteria && criteria.getCount() > 0 {
		sb.appendString(" WHERE ")
		first = true
		foreach criterium in criteria.iterateKeys() {
			if not first:
				sb.appendString(" AND ")
			sb.appendString(prepareColumnName(criterium))
			var value = criteria.get(criterium)
			if value == null {
				sb.appendString(" IS NULL")
			}
			else {
				sb.appendString(" = ?")
				params += value
			}
			first = false
		}
	}
	sb.appendCharacter(';')
	var stmt = await prepareStatement(sb.toString())
	foreach o in params {
		if o is BufferObject {
			stmt.addParamBlob((o as! BufferObject).toBuffer())
		}
		else if o is buffer {
			stmt.addParamBlob(o as! buffer)
		}
		else if o is BooleanObject {
			stmt.addParamInteger(((o as! BooleanObject).toBoolean()) ? 1 : 0)
		}
		else {
			var s = String.asValueOrNull(o)
			if not s:
				Error.throw("unsupportedDataRecordDataValue", o)
			stmt.addParamString(s)
		}
	}
	return stmt
}

func prepareDeleteStatement(table as string, criteria as DynamicMap?) async virtual as SQLStatement
{
	description [[
		Creates an instance of SQLStatement for delete statement with the specified
		database table name, an instance of DynamicMap for criteria and an instance of
		DynamicMap for values.
	]]
	var sb = new StringBuilder()
	sb.appendString("DELETE FROM ")
	sb.appendString(prepareTableName(table))
	var params = new vector<dynamic>
	if criteria && criteria.getCount() > 0 {
		sb.appendString(" WHERE ")
		var first = true
		foreach criterium in criteria.iterateKeys() {
			if not first:
				sb.appendString(" AND ")
			sb.appendString(prepareColumnName(criterium))
			var value = criteria.get(criterium)
			if value == null {
				sb.appendString(" IS NULL")
			}
			else {
				sb.appendString(" = ?")
				params += value
			}
			first = false
		}
	}
	sb.appendCharacter(';')
	var stmt = await prepareStatement(sb.toString())
	foreach o in params {
		if o is BufferObject {
			stmt.addParamBlob((o as! BufferObject).toBuffer())
		}
		else if o is buffer {
			stmt.addParamBlob(o as! buffer)
		}
		else if o is BooleanObject {
			stmt.addParamInteger(((o as! BooleanObject).toBoolean()) ? 1 : 0)
		}
		else {
			var s = String.asValueOrNull(o)
			if not s:
				Error.throw("unsupportedDataRecordDataValue", o)
			stmt.addParamString(s)
		}
	}
	return stmt
}

func prepareTableName(tableName as string) virtual as string:
	return tableName

func prepareColumnName(columnName as string) virtual as string:
	return columnName

func rollbackTransaction virtual async:
	await executeStatement(await prepareStatement("ROLLBACK;"))

func beginTransaction virtual async:
	await executeStatement(await prepareStatement("BEGIN;"))

func commitTransaction virtual async:
	await executeStatement(await prepareStatement("COMMIT;"))

func executeInTransaction(function as function<promise<void>>) virtual async
{
	await beginTransaction()
	try {
		await function()
		await commitTransaction()
	}
	catch e {
		await rollbackTransaction()
		throw e
	}
}
