
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.log
import jk.md5
import jk.json
import jk.time
import jk.random
import jk.sms
import jk.serverconfig
import jk.webserver
import jk.wsserver

class is SmsProvider:

class MyWebSocketConnectionHandler
{
	prop authenticated as bool = false
	prop connection as WebSocketServerConnection

	func sendMessage(message as buffer)
	{
		if authenticated:
			connection.sendMessage(message)
	}

	func close:
		connection.close()
}

class MyWebSocketConnectionManager
{
	var connections as vector<MyWebSocketConnectionHandler>?

	func add(connection as MyWebSocketConnectionHandler)
	{
		if not connections:
			connections = new vector<MyWebSocketConnectionHandler>
		connections += connection
	}

	func getConnectionHandler(connection as WebSocketServerConnection) as MyWebSocketConnectionHandler?
	{
		assert connections
		var handler as MyWebSocketConnectionHandler?
		foreach cc in connections {
			var ch = cc.connection
			if ch == connection {
				handler = cc
				break
			}
		}
		return handler
	}

	func sendBroadcastMessage(message as buffer)
	{
		assert connections
		foreach cc in connections:
			cc.sendMessage(message)
	}
}

class MyWebSocketServerEventHandler is WebSocketServerEventHandler
{
	var manager as MyWebSocketConnectionManager?
	var ctx as LoggingContext?

	ctor(ctx as LoggingContext):
		this.ctx = ctx

	prop secretKey:secretKeyValue as string
	{
		get {
			if not secretKeyValue:
				secretKeyValue = ServerConfig.require("SMS_SECRET_KEY")
			return secretKeyValue
		}
	}

	func sendBroadcastMessage(message as buffer)
	{
		assert manager
		manager.sendBroadcastMessage(message)
	}

	func onNewConnection(connection as WebSocketServerConnection)
	{
		Log.debug(ctx, "Incomming new connection")
		if not manager:
			manager = new MyWebSocketConnectionManager()
		var handler = new MyWebSocketConnectionHandler() {
			connection connection
		}
		manager.add(handler)
	}

	func onReceiveMessage(message as buffer, connection as WebSocketServerConnection)
	{
		assert manager
		var handler = assert manager.getConnectionHandler(connection)
		if not handler.authenticated:
			authenticateConnection(message, handler)
	}

	func authenticateConnection(data as buffer, connection as MyWebSocketConnectionHandler)
	{
		var cred = JSONParser.parseBufferAsMap(data)
		if not String.equals(secretKey, cred.getString("auth")) {
			Log.error(ctx, "Failed to authenticate connection")
			connection.close()
		}
		connection.authenticated = true
		var v = new DynamicMap()
		v.setInteger("type", 2)
		v.setString("command", "authentication_acknowledged")
		connection.sendMessage(String.toUTF8Buffer(JSONEncoder.toString(v)))
	}

	func onWSServerConnectionClosed:
		Log.debug(ctx, "On Connection closed")

	func onWSServerConnectionError(code as string, message as string):
		Log.error(ctx, "ERROR: " .. code .. ": " .. message)

	func onWSServerListening:
		Log.debug(ctx, "Web-Socket server listening...")
}

class SMSAppMessage
{
	const UNKNOWN_TYPE = 0
	const DATA_TYPE = 1

	func forDataBuffer(data as buffer) static as this
	{
		var v = new this().setDataBuffer(data)
		v.initialize()
		return v
	}

	func forDataMap(dataMap as DynamicMap) static as this
	{
		var v = new this().setDataMap(dataMap)
		v.initialize()
		return v
	}

	pvar smsId as string?
	pvar recipients as DynamicVector?
	pvar message as string?
	pvar priority as int

	var timestamp as long
	var data as buffer?
	var dataMap as DynamicMap?
	var type as int

	func generateId as string
	{
		var sb = new StringBuilder()
		sb.appendInteger(new Random().nextInteger())
		sb.appendLong(SystemClock.asSeconds())
		return "SMS-" .. MD5Encoder.encodeString(sb.toString())
	}

	func initialize
	{
		if not data:
			Error.throw("noData", this)
		dataMap = JSONParser.parseBufferAsMap(data)
		timestamp = SystemClock.asSeconds()
		smsId = generateId()
		recipients = dataMap.getDynamicVector("recipients")
		message = dataMap.getString("message")
		type = dataMap.getInteger("type")
		priority = dataMap.getInteger("priority")
	}

	func setDataBuffer(data as buffer) as this
	{
		this.data = data
		return this
	}

	func setDataMap(data as DynamicMap) as this
	{
		this.data = String.toUTF8Buffer(JSONEncoder.toString(data))
		return this
	}

	func getType as int:
		return type

	func toBuffer as buffer
	{
		if not dataMap:
			dataMap = new DynamicMap()
		dataMap.setString("smsId", smsId)
		dataMap.setObject("recipients", recipients)
		dataMap.setLongInteger("timestamp", timestamp)
		dataMap.setInteger("type", type)
		dataMap.setInteger("priority", priority)
		return String.toUTF8Buffer(JSONEncoder.toString(dataMap))
	}
}

var ctx as LoggingContext?
var engine as MyWebSocketServerEventHandler?

ctor(ctx as LoggingContext, server as WebServer)
{
	this.ctx = ctx
	var handler = new MyWebSocketServerEventHandler(ctx)
	this.engine = handler
	var smsWSServer = WebSocketServer.instance(ctx, handler)
	smsWSServer.createWSServer(server)
}

func sendSmsContent(recipientNumber as string, messageText as string) override async
{
	var smsdata = new DynamicMap()
	smsdata.setInteger("type", 1)
	smsdata.setInteger("priority", 1)
	smsdata.setString("message", messageText)
	smsdata.setObject("recipients", new DynamicVector().appendString(recipientNumber))
	Log.debug(ctx, "Sending message data `" .. JSONEncoder.toString(smsdata) .. "'")
	var message = SMSAppMessage.forDataMap(smsdata)
	if not engine:
		Error.throw("noEngine", this)
	engine.sendBroadcastMessage(message.toBuffer())
}

func sendSmsTemplate(recipientNumber as string, templateName as string, language as string, parameters as DynamicMap) override async:
	ERROR "notImplemented"
