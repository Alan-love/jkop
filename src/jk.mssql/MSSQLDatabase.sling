
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2023 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.log
import jk.sql

class is SQLDatabase abstract:

meta
{
	description [[
		Microsoft SQL Server database driver.
	]]
}

func instance(ctx as LoggingContext) private static as this
{
	IFDEF "target_dotnet_cs" {
		return new MSSQLDatabaseForDotnetCS() {
			ctx ctx
		}
	}
	ELSE {
	    ERROR "notImplemented"
	}
}

func forConnectionString(ctx as LoggingContext, connectionString as string) static as this
{
	var ss = String.split(connectionString, ':')
	if Vector.getSize(ss) < 4:
		Error.throw("invalidConnectionString", connectionString)
	var address = Vector.getValue(ss, 0)
	var username = Vector.getValue(ss, 1)
	var password = Vector.getValue(ss, 2)
	var database = Vector.getValue(ss, 3)
	if String.isEmpty(address):
		Error.throw("emptyAddress", connectionString)
	if String.isEmpty(username):
		Error.throw("emptyUsername", connectionString)
	if not password:
		password = ""
	if String.isEmpty(database):
		Error.throw("emptyDatabaseName", connectionString)
	return forConnectionDetails(ctx, address, username, password, database)
}

func forConnectionDetails(ctx as LoggingContext, serverAddress as string, username as string, password as string, database as string) static as this
{
	var v = instance(ctx)
	v.setConnectionDetails(serverAddress, username, password, database)
	return v
}

func getDatabaseTypeId override as string:
	return "mssql"

func setConnectionDetails(serverAddress as string, username as string, password as string, database as string) abstract

func checkIfTableExists(table as string) override async as bool
{
	var stmt = await prepareStatement("SELECT TABLE_NAME FROM information_schema.tables WHERE table_type = 'BASE TABLE' AND table_name = ? LIMIT 1;")
	stmt.addParamString(table)
	var sr = await getSingleRowOrNull(stmt)
	if not sr:
		return false
	return String.equals(table, sr.getString("TABLE_NAME"))
}

func getAllTableNames override async as vector<string>
{
	var stmt = await prepareStatement("SELECT TABLE_NAME FROM information_schema.tables WHERE table_type = 'BASE TABLE';")
	var it = await executeQueryStatement(stmt)
	var v = new vector<string>
	foreach vv in it {
		var nn = vv.getString("TABLE_NAME")
		if nn:
			v += nn
	}
	it.close()
	return v
}

func columnToCreateString(cc as SQLTableColumnInfo) private as string
{
	var sb = new StringBuilder()
	var columnName = cc.name
	sb.appendString(columnName)
	sb.appendCharacter(' ')
	var tt = cc.type
	if tt == SQLTableColumnInfo.TYPE_INTEGER_KEY {
		sb.appendString("INTEGER AUTO_INCREMENT, PRIMARY KEY (" .. columnName .. ")")
	}
	else if tt == SQLTableColumnInfo.TYPE_INTEGER {
		sb.appendString("INTEGER")
	}
	else if tt == SQLTableColumnInfo.TYPE_LONG_KEY {
		sb.appendString("BIGINT AUTO_INCREMENT, PRIMARY KEY (" .. columnName .. ")")
	}
	else if tt == SQLTableColumnInfo.TYPE_LONG {
		sb.appendString("BIGINT")
	}
	else if tt == SQLTableColumnInfo.TYPE_STRING {
		sb.appendString("VARCHAR(255)")
	}
	else if tt == SQLTableColumnInfo.TYPE_STRING_KEY {
		sb.appendString("VARCHAR(255), PRIMARY KEY (" .. columnName .. ")")
	}
	else if tt == SQLTableColumnInfo.TYPE_TEXT {
		sb.appendString("LONGTEXT")
	}
	else if tt == SQLTableColumnInfo.TYPE_BLOB {
		sb.appendString("LONGBLOB")
	}
	else if tt == SQLTableColumnInfo.TYPE_DOUBLE {
		sb.appendString("REAL")
	}
	else {
		Error.throw("unsupportedColumnType", columnName .. ":" .. String.forInteger(tt))
	}
	return sb.toString()
}

func prepareCreateTableStatement(table as string, columns as vector<SQLTableColumnInfo>) async override as SQLStatement
{
	var sb = new StringBuilder()
	sb.appendString("CREATE TABLE ")
	sb.appendString(table)
	sb.appendString(" (")
	var first = true
	foreach column in columns {
		if not first:
			sb.appendCharacter(',')
		sb.appendCharacter(' ')
		sb.appendString(columnToCreateString(column))
		first = false
	}
	sb.appendString(" );")
	return await prepareStatement(sb.toString())
}

func prepareDeleteTableStatement(table as string) async override as SQLStatement
{
	var sb = new StringBuilder()
	sb.appendString("DROP TABLE ")
	sb.appendString(table)
	sb.appendString(";")
	return await prepareStatement(sb.toString())
}

func getLastInsertId(table as string) override async as long
{
	var v = await getSingleRowOrThrowError(await prepareStatement("SELECT IDENT_CURRENT('" .. table .. "') AS id;"))
	return v.getLongIntegerOrDefaultValue("id", -1)
}

func getPrimaryKeyColumnName(table as string) override async as string?
{
	var v = await getSingleRowOrNull(await prepareStatement("SELECT COLUMN_NAME FROM information_schema.KEY_COLUMN_USAGE WHERE CONSTRAINT_NAME LIKE 'PK_%' AND TABLE_NAME='" .. table .. "';"))
	if not v:
		return null
	return v.getStringOrNull("COLUMN_NAME")
}

func getIdentityColumnName(table as string) override async as string?
{
	var v = await getSingleRowOrNull(await prepareStatement("SELECT name FROM sys.columns WHERE object_id = object_id('" .. table .. "') AND is_identity=1;"))
	if not v:
		return null
	return v.getStringOrNull("name")
}

func checkIfColumnExists(table as string, name as string) override async as bool
{
	// FIXME
	ERROR "notImplemented"
}

func checkIfColumnTypeMatches(table as string, column as SQLTableColumnInfo) override async as bool
{
	// FIXME
	ERROR "notImplemented"
}

func prepareCreateColumnStatement(table as string, column as SQLTableColumnInfo) async override as SQLStatement
{
	// FIXME
	ERROR "notImplemented"
}

func prepareUpdateColumnTypeStatement(table as string, column as SQLTableColumnInfo) async override as SQLStatement
{
	// FIXME
	ERROR "notImplemented"
}
