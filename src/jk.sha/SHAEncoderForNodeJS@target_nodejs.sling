
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2023 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.fs
import jk.nodejs

class is SHAEncoder:

func createHasher private as dynamic
{
	var algo as string
	if version == SHAEncoder.ANY {
		algo = "sha256"
	}
	else if version == SHAEncoder.SHA1 {
		algo = "sha1"
	}
	else if version == SHAEncoder.SHA224 {
		algo = "sha224"
	}
	else if version == SHAEncoder.SHA256 {
		algo = "sha256"
	}
	else if version == SHAEncoder.SHA384 {
		algo = "sha384"
	}
	else if version == SHAEncoder.SHA512 {
		algo = "sha512"
	}
	else {
		Error.throw("unsupportedShaAlgorithm", String.forInteger(version))
	}
	return NodeModule.require("crypto").createHash(algo)
}

func encodeBuffer(data as buffer) override as buffer
{
	if not data:
		Error.throw("nullBufferToEncode", null)
	var hasher = createHasher()
	hasher.update(data)
	var v = hasher.digest() as! buffer
	if not v:
		Error.throw("failedToComputeShaHash", data)
	return v
}

func encodeFile(file as File) override as buffer
{
	if not file:
		Error.throw("nullFileToEncode", null)
	var bsz = 10 * 1024 * 1024
	if file.getSize() < bsz:
		return encodeBuffer(file.getContentsBuffer())
	var rd = file.read()
	var hasher = createHasher()
	var buffer = malloc 1024
	loop {
		var sz = rd.read(buffer)
		if sz < 1:
			break
		var bb = buffer
		if sz < bsz:
			bb = Buffer.getSubBuffer(buffer, 0, sz)
		hasher.update(bb)
	}
	rd.close()
	var v = hasher.digest() as! buffer
	if not v:
		Error.throw("failedToComputeShaHash", file)
	return v
}
