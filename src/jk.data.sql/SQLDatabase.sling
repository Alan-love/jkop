
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2022 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.log

class abstract:

meta
{
	description [[
		SQL database connection. This is the base class and abstraction for all
		database-specific driver classes.
	]]
}

pvar logger as LoggingContext = null

func logDebug(message as string):
	Log.debug(logger, message)

func logInfo(message as string):
	Log.info(logger, message)

func logWarning(message as string):
	Log.warning(logger, message)

func logError(message as string):
	Log.error(logger, message)

// Generic methods (shared for both synchronous and asynchoronous APIs)
func getDatabaseTypeId abstract as string

// Synchronous API
func prepareSync(sql as string) abstract as SQLStatement
func prepareCreateTableStatementSync(table as string, columns as vector<SQLTableColumnInfo>) abstract as SQLStatement
func prepareDeleteTableStatementSync(table as string) abstract as SQLStatement

func prepareCreateIndexStatementSync(table as string, columns as vector<string>, unique as bool) virtual as SQLStatement
{
	meta {
		description [[
			Prepares a CREATE INDEX statement as a string for the supplied table name, column name and a boolean flag which
			indicates if the index will be unique.
		]]
	}
	assert String.isNotEmpty(table) && Vector.isNotEmpty(columns)
	var namesb = new StringBuilder()
	namesb.appendString(table)
	foreach column in columns {
		namesb.appendCharacter('_')
		namesb.appendString(column)
	}
	var idxname = namesb.toString()
	var unq = ""
	if unique:
		unq = "UNIQUE "
	var sqlsb = new StringBuilder()
	sqlsb.appendString("CREATE ")
	sqlsb.appendString(unq)
	sqlsb.appendString("INDEX ")
	sqlsb.appendString(idxname)
	sqlsb.appendString(" ON ")
	sqlsb.appendString(table)
	sqlsb.appendString(" (")
	var first = true
	foreach column in columns {
		if first {
			sqlsb.appendString(" ")
		}
		else {
			sqlsb.appendString(", ")
		}
		first = false
		sqlsb.appendString(column)
	}
	sqlsb.appendString(" )")
	return prepareSync(sqlsb.toString())
}

func prepareCreateColumnStatementSync(table as string, column as SQLTableColumnInfo) abstract as SQLStatement
func prepareUpdateColumnTypeStatementSync(table as string, column as SQLTableColumnInfo) abstract as SQLStatement
func getLastInsertIdSync(table as string) abstract async as long
func getIdentityColumnNameSync(table as string) abstract async as string
func getPrimaryKeyColumnNameSync(table as string) abstract async as string
func columnExistsSync(table as string, name as string) abstract async as bool
func isColumnTypeSameSync(table as string, column as SQLTableColumnInfo) abstract async as bool
func closeSync abstract
func executeSync(stmt as SQLStatement) abstract async as bool
func executeUpdateDeleteSync(stmt as SQLStatement) abstract async as int
func querySync(stmt as SQLStatement) abstract async as SQLResultSetIterator
func querySingleRowSync(stmt as SQLStatement) abstract async as DynamicMap
func tableExistsSync(table as string) abstract async as bool
func queryAllTableNamesSync abstract async as vector

// Asynchronous API
func close(callback as function) abstract
func execute(stmt as SQLStatement, callback as function<void, bool>) async abstract
func executeUpdateDelete(stmt as SQLStatement, callback as function<void, int>) async abstract
func query(stmt as SQLStatement, callback as function<void, SQLResultSetIterator>) async  abstract
func querySingleRow(stmt as SQLStatement, callback as function<void, DynamicMap>)async abstract
func tableExists(table as string, callback as function<void, bool>) async abstract
func queryAllTableNames(callback as function<void, vector>) async abstract

func ensureColumnsExistsSync(tableName as string, columns as vector<SQLTableColumnInfo>) async as bool
{
	meta {
		description [[
			Checks if the database table exists and creates it if it does not exist.
			Returns false if something went wrong.
		]]
	}
	foreach column as SQLTableColumnInfo in columns {
		if await columnExistsSync(tableName, column.getName()) {
			if not await isColumnTypeSameSync(tableName, column):
				assert await executeSync(prepareUpdateColumnTypeStatementSync(tableName, column))
		}
		else {
			assert await executeSync(prepareCreateColumnStatementSync(tableName, column))
		}
	}
	return true
}

func ensureTableExistsSync(table as SQLTableInfo) virtual async as bool
{
	meta {
		description [[
			Checks if the database table exists and creates it if it does not exist.
		]]
	}
	assert table
	var name = table.getName()
	assert String.isNotEmpty(name)
	if await tableExistsSync(name) {
		var v = await ensureColumnsExistsSync(name, table.getColumns())
		if v:
			return true
		return false
	}
	assert await executeSync(prepareCreateTableStatementSync(name, table.getColumns()))
	foreach cii as SQLTableColumnIndexInfo in table.getIndexes() {
		if not await executeSync(prepareCreateIndexStatementSync(name, cii.getColumns(), cii.getUnique())):
			await executeSync(prepareDeleteTableStatementSync(name))
	}
	return true
}

func ensureTableExists(table as SQLTableInfo, callback as function<void, bool>) async virtual
{
	meta {
		description [[
			Checks if the database table exists and creates it if it does not exist.
			Calls the callback function parameter if supplied and passes the result of the operation.
		]]
	}
	var v = await ensureTableExistsSync(table)
	if callback:
		callback(v)
}

func createColumnSelectionString(columns as vector<string>) private as string
{
	meta {
		description [[
			Creates the column selection part of an SQL select statement
			in string from the supplied array of column name(s).
		]]
	}
	if not columns || sizeof columns < 1:
		return "*"
	var sb = new StringBuilder()
	var first = true
	foreach column in columns {
		if not first:
			sb.appendString(", ")
		sb.appendString(column)
		first = false
	}
	return sb.toString()
}

func createOrderByString(order as vector<SQLOrderingRule>) as string
{
	meta {
		description [[
			Creates the order by part of an SQL select statement
			in string from the supplied array of column name(s)
			or instance(s) of SQLOrderingRule class.
		]]
	}
	assert order && sizeof order > 0
	var sb = new StringBuilder()
	sb.appendString(" ORDER BY ")
	var first = true
	foreach sr in order {
		if not first:
			sb.appendString(", ")
		sb.appendString(sr.getColumn())
		sb.appendCharacter(' ')
		if sr.getDescending() {
			sb.appendString("DESC")
		}
		else {
			sb.appendString("ASC")
		}
		first = false
	}
	return sb.toString()
}

func prepareQueryAllStatementSync(table as string, columns as vector<string> = null, order as vector<SQLOrderingRule> = null) virtual as SQLStatement
{
	meta {
		description [[
			Creates an instance of SQLStatement for select statement with the specified
			database table name, an array of column name(s) and an array of column
			name(s) or instance(s) of SQLOrderingRule for order by clause.
		]]
	}
	var sb = new StringBuilder()
	sb.appendString("SELECT ")
	sb.appendString(createColumnSelectionString(columns))
	sb.appendString(" FROM ")
	sb.appendString(table)
	sb.appendString(createOrderByString(order))
	sb.appendString(";")
	return prepareSync(sb.toString())
}

func prepareCountRecordsStatementSync(table as string, criteria as DynamicMap, likeOperator as bool = false) virtual as SQLStatement
{
	meta {
		description [[
			Creates an instance of SQLStatement for select count statement
			with the specified database table name and a map of criteria.
		]]
	}
	var sb = new StringBuilder()
	sb.appendString("SELECT COUNT(*) AS count FROM ")
	sb.appendString(table)
	var first = true
	var keys as vector<string>
	if criteria {
		keys = criteria.getKeys()
		foreach key as string in keys {
			if first {
				sb.appendString(" WHERE ")
				first = false
			}
			else {
				sb.appendString(" AND ")
			}
			sb.appendString(key)
			var value = criteria.getString(key)
			if value == null {
				sb.appendString(" IS NULL")
			}
			else {
				if likeOperator {
					sb.appendString(" LIKE ?")
				}
				else {
					sb.appendString(" = ?")
				}
			}
		}
	}
	sb.appendCharacter(';')
	var stmt = assert prepareSync(sb.toString())
	if keys {
		foreach key as string in keys {
			var val = criteria.getString(key)
			if val == null:
				continue
			if likeOperator {
				stmt.addParamString("%" .. val .. "%")
			}
			else {
				stmt.addParamString(val)
			}
		}
	}
	return stmt
}

func prepareQueryWithCriteriaStatementSync(table as string, criteria as DynamicMap, limit as int = 0, offset as int = 0, columns as vector<string> = null, order as vector<SQLOrderingRule> = null, likeOperator as bool = false) virtual as SQLStatement
{
	meta {
		description [[
			Creates an instance of SQLStatement for select statement
			with the specified database table name, a map of criteria,
			limit, offset, an array of column name(s), an array of column
			name(s) or instance(s) of SQLOrderingRule for order by clause
			and a flag to use LIKE operator.
		]]
	}
	var sb = new StringBuilder()
	sb.appendString("SELECT ")
	sb.appendString(createColumnSelectionString(columns))
	sb.appendString(" FROM ")
	sb.appendString(table)
	var first = true
	var keys as vector<string>
	if criteria {
		keys = criteria.getKeys()
		foreach key as string in keys {
			if first {
				sb.appendString(" WHERE ")
				first = false
			}
			else {
				sb.appendString(" AND ")
			}
			sb.appendString(key)
			var value = criteria.getString(key)
			if value == null {
				sb.appendString(" IS NULL")
			}
			else {
				if likeOperator {
					sb.appendString(" LIKE ?")
				}
				else {
					sb.appendString(" = ?")
				}
			}
		}
	}
	sb.appendString(createOrderByString(order))
	if limit > 0 {
		sb.appendString(" LIMIT ")
		sb.appendString(String.forInteger(limit))
	}
	if offset > 0 {
		sb.appendString(" OFFSET ")
		sb.appendString(String.forInteger(offset))
	}
	sb.appendCharacter(';')
	var sql = sb.toString()
	var stmt = prepareSync(sql)
	if not stmt:
		return null
	if keys {
		foreach key as string in keys {
			var val = criteria.getString(key)
			if val == null:
				continue
			if likeOperator {
				stmt.addParamString("%" .. val .. "%")
			}
			else {
				stmt.addParamString(val)
			}
		}
	}
	return stmt
}

func prepareQueryDistinctValuesStatementSync(table as string, column as string) virtual as SQLStatement
{
	meta {
		description [[
			Creates an instance of SQLStatement for query distinct values
			with the specified database table name and column name.
		]]
	}
	assert String.isNotEmpty(table) && String.isNotEmpty(column)
	var sb = new StringBuilder()
	sb.appendString("SELECT DISTINCT ")
	sb.appendString(column)
	sb.appendString(" FROM ")
	sb.appendString(table)
	sb.appendString(";")
	return prepareSync(sb.toString())
}

func prepareSelectStatementSync(table as string, criteria as DynamicMap) virtual as SQLStatement
{
	meta {
		description [[
			Creates an instance of SQLStatement for select all statement with the
			specified database table name and an instance of DynamicMap for criteria.
		]]
	}
	return prepareQueryWithCriteriaStatementSync(table, criteria)
}

func prepareInsertStatementSync(table as string, data as DynamicMap) virtual as SQLStatement
{
	meta {
		description [[
			Creates an instance of SQLStatement for insert statement with the
			specified database table name and an instance of DynamicMap for values.
		]]
	}
	assert String.isNotEmpty(table) && data && data.getCount() > 0
	var sb = new StringBuilder()
	sb.appendString("INSERT INTO ")
	sb.appendString(table)
	sb.appendString(" ( ")
	var first = true
	var keys = data.getKeys()
	foreach key in keys {
		if not first:
			sb.appendCharacter(',')
		sb.appendString(key as string)
		first = false
	}
	sb.appendString(" ) VALUES ( ")
	first = true
	foreach key in keys {
		if not first:
			sb.appendCharacter(',')
		if data.get(key) == null {
			sb.appendString("NULL")
		}
		else {
			sb.appendCharacter('?')
		}
		first = false
	}
	sb.appendString(" );")
	var stmt = assert prepareSync(sb.toString())
	foreach key in keys {
		var o = data.get(key)
		if o == null:
			continue
		if o is string || o is StringObject {
			stmt.addParamString(String.asString(o))
		}
		else if o is IntegerObject {
			stmt.addParamInteger(Integer.asInteger(o))
		}
		else if o is LongIntegerObject {
			stmt.addParamLongInteger(LongInteger.asLong(o))
		}
		else if o is DoubleObject {
			stmt.addParamDouble(Double.asDouble(o))
		}
		else if o is BufferObject {
			stmt.addParamBlob((o as! BufferObject).toBuffer())
		}
		else if o is buffer {
			stmt.addParamBlob(o as buffer)
		}
		else if o is BooleanObject {
			stmt.addParamInteger(((o as! BooleanObject).toBoolean()) ? 1 : 0)
		}
		else {
			var s = String.asString(o)
			if not s:
				s = ""
			stmt.addParamString(s)
		}
	}
	return stmt
}

func prepareUpdateStatementSync(table as string, criteria as DynamicMap, data as DynamicMap) virtual as SQLStatement
{
	meta {
		description [[
			Creates an instance of SQLStatement for update statement with the
			specified database table name, an instance of DynamicMap for
			criteria and an instance of DynamicMap for values.
		]]
	}
	assert String.isNotEmpty(table) && data && data.getCount() > 0
	var sb = new StringBuilder()
	sb.appendString("UPDATE ")
	sb.appendString(table)
	sb.appendString(" SET ")
	var params = new vector<object>
	var first = true
	var keys as Iterator<string> #force = data.iterateKeys()
	while keys {
		var key = keys.next()
		if not key:
			break
		if not first:
			sb.appendString(", ")
		sb.appendString(key)
		var value = data.get(key)
		if value == null {
			sb.appendString(" = NULL")
		}
		else {
			sb.appendString(" = ?")
			params += value
		}
		first = false
	}
	if criteria && criteria.getCount() > 0 {
		sb.appendString(" WHERE ")
		first = true
		var criterias as Iterator<string> #force = criteria.iterateKeys()
		while criterias {
			var criterium = criterias.next()
			if not criterium:
				break
			if not first:
				sb.appendString(" AND ")
			sb.appendString(criterium)
			var value = criteria.get(criterium)
			if value == null {
				sb.appendString(" IS NULL")
			}
			else {
				sb.appendString(" = ?")
				params += value
			}
			first = false
		}
	}
	sb.appendCharacter(';')
	var stmt = assert prepareSync(sb.toString())
	foreach o in params {
		if o is BufferObject {
			stmt.addParamBlob((o as! BufferObject).toBuffer())
		}
		else if o is buffer {
			stmt.addParamBlob(o as! buffer)
		}
		else if o is BooleanObject {
			stmt.addParamInteger(((o as! BooleanObject).toBoolean()) ? 1 : 0)
		}
		else {
			var s = String.asString(o)
			if not s:
				s = ""
			stmt.addParamString(s)
		}
	}
	return stmt
}

func prepareDeleteStatementSync(table as string, criteria as DynamicMap) virtual as SQLStatement
{
	meta {
		description [[
			Creates an instance of SQLStatement for delete statement with the
			specified database table name, an instance of DynamicMap for
			criteria and an instance of DynamicMap for values.
		]]
	}
	assert String.isNotEmpty(table)
	var sb = new StringBuilder()
	sb.appendString("DELETE FROM ")
	sb.appendString(table)
	var params = new vector<object>
	if criteria && criteria.getCount() > 0 {
		sb.appendString(" WHERE ")
		var first = true
		var criterias as Iterator<string> #force = criteria.iterateKeys()
		while criterias {
			var criterium = criterias.next()
			if not criterium:
				break
			if not first:
				sb.appendString(" AND ")
			sb.appendString(criterium)
			sb.appendString(" = ?")
			first = false
			params += criteria.get(criterium)
		}
	}
	sb.appendCharacter(';')
	var stmt = prepareSync(sb.toString())
	if not stmt:
		return null
	foreach o in params {
		if o is BufferObject {
			stmt.addParamBlob((o as! BufferObject).toBuffer())
		}
		else if o is buffer {
			stmt.addParamBlob(o as! buffer)
		}
		else if o is BooleanObject {
			stmt.addParamInteger(((o as! BooleanObject).toBoolean()) ? 1 : 0)
		}
		else {
			var s = String.asString(o)
			if not s:
				s = ""
			stmt.addParamString(s)
		}
	}
	return stmt
}
