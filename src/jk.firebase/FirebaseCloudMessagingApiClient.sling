
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.log
import jk.json
import jk.apiclient

class is ApiClient:

func forFcmServerKey(ctx as LoggingContext, fcmServerKey as string) static as this:
	return new this() {
		ctx ctx
		fcmServerKey fcmServerKey
	}

prop fcmServerKey as string

func send(registrationIds as DynamicVector, notification as DynamicMap?, data as DynamicMap?) async throws Exception as DynamicMap
{
	var body = new DynamicMap()
	var sz = registrationIds.getSize()
	if sz < 1 {
		Error.throw("noFirebaseRegistrationIds", null)
	}
	else if sz == 1 {
		body.setString("to", String.asValueOrThrowError(registrationIds.get(0)))
	}
	else {
		body.setObject("registration_ids", registrationIds)
	}
	body.setString("priority", "high")
	body.setBoolean("content_available", true)
	if notification:
		body.setObject("notification", notification)
	if data:
		body.setObject("data", data)
	var v = new JSONEncoder()
	v.setNiceFormatting(false)
	var json = v.execute(body)
	var headers = new KeyValueList<string,string>()
	headers.add("Content-Type", "application/json")
	headers.add("Authorization", "key=" .. fcmServerKey)
	var resp = await requestAndValidate("POST", "https://fcm.googleapis.com/fcm/send", headers, String.toUTF8Buffer(json))
	return JSONParser.parseBufferAsMap(resp.body)
}
