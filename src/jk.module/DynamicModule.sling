
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.log
import jk.fs

class:

IFDEF "target_dotnet_cs"
{
	depend "nuget:System.Reflection@4.3.*"
	depend "nuget:System.Runtime.Loader@4.3.*"
}

func forFile(ctx as LoggingContext, file as File) static as this
{
	var v = new this()
	v.loadFile(ctx, file)
	return v
}

func forObject(ctx as LoggingContext, oo as object #preserve) static as this
{
	IFDEF "target_dotnet_cs" {
		var asm as [magical:System.Reflection.Assembly]? #preserve
		lang "cs" {{{
			var type = oo.GetType();
			if(type != null) {
				asm = System.Reflection.IntrospectionExtensions.GetTypeInfo(type).Assembly;
			}
		}}}
		if not asm:
			Error.throw("failedToGetAssemblyObject", null)
		var v = new this()
		v.setAssembly(asm)
		return v
	}
	ELSE {
		ERROR "notImplemented"
	}
}

func isModuleFile(ctx as LoggingContext, file as File) static as bool
{
	IFDEF "target_dotnet_cs" {
		if file.hasExtension("dll"):
			return true
	}
	ELSE IFDEF "target_swift" {
		if file.hasExtension("dylib") || file.hasExtension("tbd"):
			return true
	}
	return false
}

IFDEF "target_dotnet_cs"
{
	pvar assembly as [magical:System.Reflection.Assembly]? #preserve
}

IFDEF "target_swift"
{
	pvar handle as [magical:UnsafeMutableRawPointer]?
}

func getModuleDescription as string
{
	IFDEF "target_dotnet_cs" {
		if assembly:
			return lang "cs" as string {{{ assembly.FullName }}}
	}
	return "builtin"
}

func loadFile(ctx as LoggingContext, file as File #preserve)
{
	if not file.isFile():
		Error.throw("fileDoesNotExist", file)
	ctx.logDebug("Loading dynamic module: `" .. file.getPath() .. "'")
	IFDEF "target_uwp" {
		ERROR "notImplemented"
	}
	ELSE IFDEF "target_dotnet_cs" {
		var error as string? #preserve
		lang "cs" {{{
			try {
				assembly = System.Runtime.Loader.AssemblyLoadContext.Default.LoadFromAssemblyPath(file.getPath());
			}
			catch(System.Exception e) {
				error = e.ToString();
			}
		}}}
		if assembly == null {
			if error:
				ctx.logDebug(error)
			Error.throw("failedToLoadAssembly", file)
		}
		ctx.logDebug("Assembly successfully loaded: `" .. file.getPath() .. "'")
		return
	}
	ELSE IFDEF "target_swift" {
		var path = file.getPath()
		lang "swift" {{{
			handle = dlopen(path, RTLD_NOW)
			if (handle == nil) {
				let errorString = String(validatingUTF8: dlerror())
				ctx.logDebug("Failed to load library: `" + (errorString)! + "'")
				return false
			}
		}}}
		ctx.logDebug("Assembly successfully loaded: `" .. path .. "'")
		return true
	}
	ELSE {
		ERROR "notImplemented"
	}
}

func executeStaticMethod(ctx as LoggingContext, entityName as string #preserve, methodName as string #preserve, parameters as array<object> #preserve) as bool
{
	IFDEF "target_uwp" {
		ERROR "notSupported"
	}
	ELSE IFDEF "target_dotnet_cs" {
		assert assembly:
			ctx.logError("executeStaticMethod: No assembly has been loaded.")
		var mainType as [magical:System.Type]? #preserve
		lang "cs" {{{
			mainType = assembly.GetType(entityName);
		}}}
		assert mainType:
			ctx.logError("Failed to find class `" .. entityName .. "' in dynamic module: `" .. getModuleDescription() .. "'")
		var error as string? #preserve
		var methodRef as [magical:System.Reflection.MethodInfo]? #preserve
		lang "cs" {{{
			try {
				methodRef = System.Reflection.IntrospectionExtensions.GetTypeInfo(mainType).GetMethod(methodName);
			}
			catch(System.Exception e) {
				error = e.ToString();
			}
		}}}
		if methodRef == null {
			if error:
				ctx.logDebug(error)
			ctx.logError("Failed to find method `" .. methodName .. "' in entity `" .. entityName .. "' in module: `" .. getModuleDescription() .. "'")
			return false
		}
		lang "cs" {{{
			try {
				methodRef.Invoke(null, parameters);
			}
			catch(System.Exception e) {
				error = e.ToString();
			}
		}}}
		if error {
			ctx.logDebug(error)
			ctx.logError("Failed to call method `" .. methodName .. "' in entity `" .. entityName .. "' in module: `" .. getModuleDescription() .. "'")
			return false
		}
		return true
	}
	ELSE IFDEF "target_swift" {
		if parameters != null {
			ctx.logError("Function with parameters is not available.")
			return false
		}
		else {
			lang "swift" {{{
				let sym = dlsym(handle, methodName)
				if (sym == nil) {
					let errorString = String(validatingUTF8: dlerror())
					ctx.logError("Failed to find method: `" + (errorString)! + "'")
				}
				typealias randomFunc = @convention(c) () -> AnyObject
				let f = unsafeBitCast(sym, to: randomFunc.self)
				f()
				dlclose(handle)
			}}}
			return true
		}
	}
	ELSE {
		ERROR "notImplemented"
	}
}

func createObject(ctx as LoggingContext, className as string #preserve) as object
{
	IFDEF "target_uwp" {
		ERROR "notImplemented"
	}
	ELSE IFDEF "target_dotnet_cs" {
		if not assembly:
			ERROR "noAssemblyObjectLoaded"
		var type as [magical:System.Type]? #preserve
		lang "cs" {{{
			type = assembly.GetType(className);
		}}}
		if not type:
			Error.throw("failedToFindClassInDynamicModule", className)
		var constructor as [magical:System.Reflection.ConstructorInfo]? #preserve
		lang "cs" {{{
			constructor = System.Reflection.IntrospectionExtensions.GetTypeInfo(type).GetConstructor(new System.Type[0]);
		}}}
		if constructor == null:
			Error.throw("failedToFindConstructorInClass", className)
		var v = lang "cs" as object? {{{ constructor.Invoke(null) }}}
		if not v:
			Error.throw("failedToCreateObject", className)
		return v
	}
	ELSE IFDEF "target_swift" {
		// FIXME: Need for appropriate API
		ERROR "notImplemented"
	}
	ELSE {
		ERROR "notImplemented"
	}
}
