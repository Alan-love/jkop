
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.url

class:

func getPageUrlStringOrNull static as string?
{
	IFDEF "target_browser_js" {
		var url as string?
		lang "js" {{{
			url = window.location.href;
		}}}
		return url
	}
	ELSE {
		return null
	}
}

func getPageUrlStringOrThrowError static throws Exception as string
{
	var v = getPageUrlStringOrNull()
	if not v:
		Error.throw("noPageUrl")
	return v
}

func getPageUrl static as URL:
	return URL.forString(getPageUrlStringOrNull() ?? "")

func getPageHashOrNull static as string?
{
	IFDEF "target_browser_js" {
		var hash as string?
		lang "js" {{{
			hash = window.location.hash;
		}}}
		return hash
	}
	ELSE {
		return null
	}
}

func getPageHashOrThrowError static throws Exception as string
{
	var v = getPageHashOrNull()
	if not v:
		Error.throw("noPageHash")
	return v
}

func getQueryStringOrNull static as string?
{
	IFDEF "target_browser_js" {
		var url as string?
		lang "js" {{{
			url = window.location.href;
		}}}
		if String.isEmpty(url):
			return null
		var q = String.getIndexOfCharacter(url, '?')
		if q < 0:
			return null
		var v = String.getEndOfString(url, q + 1)
		var hash = String.getIndexOfCharacter(v, '#')
		if hash >= 0:
			v = String.getSubString(v, 0, hash)
		return v
	}
	ELSE {
		return null
	}
}

func getQueryStringOrThrowError static throws Exception as string
{
	var v = getQueryStringOrNull()
	if not v:
		Error.throw("noQueryString")
	return v
}

func getQueryStringValueOrNull(key as string) static as string?
{
	var qss = getQueryStringOrNull()
	if String.isEmpty(qss):
		return null
	var qs = QueryString.parse(qss)
	return Map.getValue(qs, key)
}

func getQueryStringValueOrThrowError(key as string) static throws Exception as string
{
	var v = getQueryStringValueOrNull(key)
	if not v:
		Error.throw("queryStringParameterNotSpecified", key)
	return v
}
