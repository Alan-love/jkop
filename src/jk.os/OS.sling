
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class:

description [[
	Operating system and architecture detection functionality.
]]

func getCurrentPlatformName static as string:
	return VALUE "targetPlatformName"

IFDEF "target_android"
{
}
ELSE IFDEF "target_apple"
{
}
ELSE IFDEF "target_browser"
{
}
ELSE IFDEF "target_windows"
{
}
ELSE {
	var isWindowsFlag static as BooleanObject?
	var isLinuxFlag static as BooleanObject?
	var isMacOSFlag static as BooleanObject?
	var isAndroidFlag static as BooleanObject?
	var isIOSFlag static as BooleanObject?
}

func is32Bit static as bool
{
	var cpu = getProcessorType()
	if cpu == "x86" || cpu == "arm":
		return true
	return false
}

func is64Bit static as bool
{
	var cpu = getProcessorType()
	if cpu == "x64" || cpu == "arm64":
		return true
	return false
}

func getProcessorType static as string
{
	IFDEF "target_dotnet_cs" {
		lang "cs" {{{
			if(System.Runtime.InteropServices.RuntimeInformation.OSArchitecture == System.Runtime.InteropServices.Architecture.X86) {
				return "x86";
			}
			if(System.Runtime.InteropServices.RuntimeInformation.OSArchitecture == System.Runtime.InteropServices.Architecture.X64) {
				return "x64";
			}
			if(System.Runtime.InteropServices.RuntimeInformation.OSArchitecture == System.Runtime.InteropServices.Architecture.Arm) {
				return "arm";
			}
			if(System.Runtime.InteropServices.RuntimeInformation.OSArchitecture == System.Runtime.InteropServices.Architecture.Arm64) {
				return "arm64";
			}
		}}}
	}
	return "unknown"
}

func isWindows static as bool
{
	IFDEF "target_android" {
		return false
	}
	ELSE IFDEF "target_apple" {
		return false
	}
	ELSE IFDEF "target_browser" {
		return false
	}
	ELSE IFDEF "target_windows" {
		return true
	}
	ELSE {
		if isWindowsFlag:
			return isWindowsFlag.toBoolean()
		var v = isSystemType("windows")
		isWindowsFlag = Boolean.asObject(v)
		return v
	}
}

func isLinux static as bool
{
	IFDEF "target_android" {
		return false
	}
	ELSE IFDEF "target_apple" {
		return false
	}
	ELSE IFDEF "target_browser" {
		return false
	}
	ELSE IFDEF "target_windows" {
		return false
	}
	ELSE {
		if isLinuxFlag:
			return isLinuxFlag.toBoolean()
		var v = isSystemType("linux")
		isLinuxFlag = Boolean.asObject(v)
		return v
	}
}

func isMacOS static as bool
{
	IFDEF "target_android" {
		return false
	}
	ELSE IFDEF "target_macos" {
		return true
	}
	ELSE IFDEF "target_ios" {
		return false
	}
	ELSE IFDEF "target_browser" {
		return false
	}
	ELSE IFDEF "target_windows" {
		return false
	}
	ELSE {
		if isMacOSFlag:
			return isMacOSFlag.toBoolean()
		var v = isSystemType("macos")
		isMacOSFlag = Boolean.asObject(v)
		return v
	}
}

func isOSX static as bool:
	return isMacOS()

func isAndroid static as bool
{
	IFDEF "target_android" {
		return true
	}
	ELSE IFDEF "target_apple" {
		return false
	}
	ELSE IFDEF "target_browser" {
		return false
	}
	ELSE IFDEF "target_windows" {
		return false
	}
	ELSE {
		if isAndroidFlag:
			return isAndroidFlag.toBoolean()
		var v = isSystemType("android")
		isAndroidFlag = Boolean.asObject(v)
		return v
	}
}

func isIOS static as bool
{
	IFDEF "target_android" {
		return false
	}
	ELSE IFDEF "target_macos" {
		return false
	}
	ELSE IFDEF "target_ios" {
		return true
	}
	ELSE IFDEF "target_browser" {
		return false
	}
	ELSE IFDEF "target_windows" {
		return false
	}
	ELSE {
		if isIOSFlag:
			return isIOSFlag.toBoolean()
		var v = isSystemType("ios")
		isIOSFlag = Boolean.asObject(v)
		return v
	}
}

func isSystemType(ida as string) static as bool
{
	var id = ida
	if id == "osx":
		id = "macos"
	IFDEF "target_dotnet_cs" {
		if id == "windows" {
			var c as int
			lang "cs" {{{
				c = System.IO.Path.DirectorySeparatorChar;
			}}}
			if c == '\\':
				return true
			return false
		}
		if id == "macos" {
			if isDirectory("/Applications"):
				return true
			return false
		}
		if id == "posix" || id == "linux" || id == "unix" {
			if isFile("/bin/sh") && isDirectory("/Applications") == false:
				return true
			return false
		}
	}
	ELSE IFDEF "target_java" {
		var v = false
		lang "java" {{{
			java.lang.String os = java.lang.System.getProperty("os.name").toLowerCase();
		}}}
		if id == "windows" {
			lang "java" {{{
				if(os.indexOf("win") >= 0) {
					v = true;
				}
			}}}
			return v
		}
		if id == "macos" {
			lang "java" {{{
				if(os.indexOf("mac") >= 0) {
					v = true;
				}
			}}}
			if v:
				return v
			if isSystemType("posix") == false:
				return false
			if isDirectory("/Applications"):
				return true
			return false
		}
		if id == "posix" || id == "linux" || id == "unix" {
			lang "java" {{{
				if(os.indexOf("nix") >= 0 || os.indexOf("nux") >= 0 || os.indexOf("aix") > 0) {
					v = true;
				}
			}}}
			return v
		}
	}
	ELSE IFDEF "target_node_js" {
		var platform = lang "js" as string {{{ process.platform }}}
		if id == "windows" {
			if String.equals(platform, "win32"):
				return true
			return false
		}
		if id == "linux" {
			if String.equals(platform, "linux"):
				return true
			return false
		}
		if id == "macos" {
			if String.equals(platform, "darwin"):
				return true
			return false
		}
	}
	ELSE IFDEF "target_sushi" {
		var type = lang "lua" as string {{{ _os:get_system_type() }}}
		return type == id
	}
	ELSE IFDEF "target_swift" {
		if id == "windows" {
			if isDirectory("%userprofile%"):
				return true
			return false
		}
		if id == "macos" {
			if isDirectory("/Applications"):
				return true
			return false
		}
		if id == "posix" || id == "linux" || id == "unix" {
			if isFile("/bin/sh") && isDirectory("/Applications") == false:
				return true
			return false
		}
	}
	return false
}

func isDirectory(path as string) private static as bool #used
{
	assert String.isNotEmpty(path)
	IFDEF "target_dotnet_cs" {
		var v as bool
		lang "cs" (path) {{{
			var attrs = System.IO.File.GetAttributes(path);
			v = attrs.HasFlag(System.IO.FileAttributes.Directory);
		}}}
		return v
	}
	ELSE IFDEF "target_swift" {
		var v as bool #mutated
		var dir as bool #mutated
		lang "swift" {{{
			var isDir: ObjCBool = false
			v = FileManager.default.fileExists(atPath:path, isDirectory:&isDir)
			dir = isDir.boolValue
		}}}
		if not v:
			return false
		return dir
	}
	ELSE IFDEF "target_java" {
		return lang "java" (path) as bool {{{ new java.io.File(path).isDirectory() }}}
	}
	ELSE IFDEF "target_node_js" {
		return lang "js" (path) as bool {{{ require("fs").statSync(path).isDirectory() }}}
	}
	ELSE IFDEF "target_browser" {
		nop path
		ERROR NOT_SUPPORTED
	}
	ELSE {
		nop path
		ERROR NOT_IMPLEMENTED
	}
}

func isFile(path as string) private static as bool #used
{
	assert String.isNotEmpty(path)
	IFDEF "target_dotnet_cs" {
		var v as bool
		lang "cs" {{{
			try {
				var attrs = System.IO.File.GetAttributes(path);
				v = attrs.HasFlag(System.IO.FileAttributes.Directory) == false;
			}
			catch(System.Exception e) {
				System.Console.WriteLine(e.ToString());
			}
		}}}
		return v
	}
	ELSE IFDEF "target_swift" {
		/*[
		var v as bool
		var d as bool
		lang "swift" {{{
			var manager: FileManager = FileManager.default
			var isDir : ObjCBool = false
			v = manager?.fileExists(atPath:(path as NSString?)?.standardizingPath!, isDirectory:&isDir) ?? false
			d = !isDir.boolValue
		}}}
		if not v:
			return false
		return d
		]*/

		ERROR NOT_IMPLEMENTED
	}
	ELSE IFDEF "target_java" {
		return lang "java" as bool {{{ new java.io.File(path).isFile() }}}
	}
	ELSE IFDEF "target_node_js" {
		return lang "js" (path) as bool {{{ require("fs").statSync(path).isFile() }}}
	}
	ELSE IFDEF "target_browser" {
		nop path
		ERROR NOT_SUPPORTED
	}
	ELSE {
		nop path
		ERROR NOT_IMPLEMENTED
	}
}
