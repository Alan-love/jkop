
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.log
import jk.nodejs

class is RedisClient:

depend "npm:@redis/client:1.5.5"

var redisClient as dynamic?

func connect(redisAddress as string) async override
{
	await disconnect()
	var api = NodeModule.require("@redis/client")
	redisClient = api.createClient!({
		url redisAddress
		socket {
			reconnectStrategy func as dynamic => 1000
		}
	})
	if not redisClient:
		Error.throw("failedToCreateRedisClient", null)
	redisClient.on!("error", func => redisClient = null)
	await redisClient.connect!()
}

func disconnect async override
{
	if redisClient {
		await redisClient.disconnect!()
		redisClient = null
	}
}

func set(key as string, value as string, options as map<string,dynamic>) async override
{
	assert redisClient
	if ctx.isInDebugMode():
		Log.debug(ctx, "Redis SET `" .. key .. "' = `" .. value .. "'")
	redisClient.set!(key, value, lang "js" as dynamic {{{ Object.fromEntries(options) }}})
}

func get(key as string) async override as string?
{
	assert redisClient
	var value = String.asValueOrNull(await redisClient.get!(key))
	if ctx.isInDebugMode():
		Log.debug(ctx, "Redis GET `" .. key .. "' = `" .. value ?? "" .. "'")
	return value
}
