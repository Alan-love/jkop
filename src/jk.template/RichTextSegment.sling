
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2023 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.xml

class:

pvar text as string?
pvar bold as bool
pvar italic as bool
pvar underline as bool
pvar color as string?
pvar link as string?
pvar reference as string?
pvar isInline = false
pvar linkPopup = false

func addMarkupModifiers(sb as StringBuilder)
{
	if bold:
		sb.appendString("**")
	if italic:
		sb.appendString("''")
	if underline:
		sb.appendString("__")
}

func toMarkup as string?
{
	var sb = new StringBuilder()
	addMarkupModifiers(sb)
	if String.isNotEmpty(link) {
		sb.appendCharacter('[')
		if isInline:
			sb.appendCharacter('>')
		sb.appendString(link)
		if String.isNotEmpty(text) {
			sb.appendCharacter('|')
			sb.appendString(text)
		}
		sb.appendCharacter(']')
	}
	else if String.isNotEmpty(reference) {
		sb.appendCharacter('{')
		if isInline:
			sb.appendCharacter('>')
		sb.appendString(reference)
		if String.isNotEmpty(text) {
			sb.appendCharacter('|')
			sb.appendString(text)
		}
		sb.appendCharacter('}')
	}
	else {
		if text:
			sb.appendString(text)
	}
	addMarkupModifiers(sb)
	return sb.toString()
}

func toJson as DynamicMap
{
	var v = new DynamicMap()
	v.setString("text", text)
	if isInline:
		v.setBoolean("inline", isInline)
	if bold:
		v.setBoolean("bold", bold)
	if italic:
		v.setBoolean("italic", italic)
	if underline:
		v.setBoolean("underline", underline)
	if String.isEmpty(color) == false:
		v.setString("color", color)
	if String.isEmpty(link) == false:
		v.setString("link", link)
	if String.isEmpty(reference) == false:
		v.setString("reference", reference)
	return v
}

func toHtmlWithResolvedReference(resolvedReference as string?) as string
{
	var sb = new StringBuilder()
	var aOpen = false
	var text = getText()
	var link = getLink()
	if String.isNotEmpty(link) {
		if getIsInline() {
			// FIXME: Should allow other content types as well, not just images
			sb.appendString("<img src=\"" .. HTMLString.sanitize(link) .. "\" />")
		}
		else {
			var targetblank = ""
			if getLinkPopup():
				targetblank = " target=\"_blank\""
			sb.appendString("<a" .. targetblank .. " class=\"urlLink\" href=\"" .. HTMLString.sanitize(link) .. "\">")
			aOpen = true
		}
	}
	if String.isNotEmpty(resolvedReference) {
		if String.isEmpty(text):
			text = resolvedReference
		sb.appendString("<a class=\"referenceLink\" href=\"" .. HTMLString.sanitize(resolvedReference) .. "\">")
		aOpen = true
	}
	if getIsInline() == false {
		if getBold():
			sb.appendString("<b>")
		if getItalic():
			sb.appendString("<i>")
		if getUnderline():
			sb.appendString("<u>")
		var color = getColor()
		if String.isNotEmpty(color):
			sb.appendString("<span style=\"color: " .. HTMLString.sanitize(color) .. ";\">")
		if text:
			sb.appendString(HTMLString.sanitize(text))
		if String.isNotEmpty(color):
			sb.appendString("</span>")
		if getUnderline():
			sb.appendString("</u>")
		if getItalic():
			sb.appendString("</i>")
		if getBold():
			sb.appendString("</b>")
	}
	if aOpen:
		sb.appendString("</a>")
	return sb.toString()
}

func toHtmlWithResolver(refs as RichTextDocumentReferenceResolver?) async as string
{
	var ref = getReference()
	var href as string?
	if refs {
		href = await refs.getReferenceHref(ref)
		if String.isEmpty(text):
			text = await refs.getReferenceTitle(ref)
	}
	else {
		href = ref
	}
	return toHtmlWithResolvedReference(href)
}

func toHtml as string:
	return toHtmlWithResolvedReference(getReference())
