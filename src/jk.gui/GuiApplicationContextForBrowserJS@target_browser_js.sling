
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.fs
import jk.gfx
import jk.base64

class is GuiApplicationContext:

class MyTimerObject is GuiApplicationContext.TimerObject
{
	pvar timerID as int

	func cancel implement:
		lang "js" {{{
			 clearTimeout(this.timerID); 
		}}}
}

var instance static as this?
var imageCache = new map<string,Image>
var screenDensity = 0

func getInstance static as this
{
	if not instance:
		instance = new this()
	return instance
}

func clearResources:
	imageCache = new map<string,Image>

func prepareResources(resources as array<string>, callback as function<void>?)
{
	if sizeof resources < 1 {
		if callback:
			callback()
		return
	}
	var loadedResources = 0
	var numberOfFonts = 0
	var totalResources = sizeof resources
	var onResourceLoadingComplete = func {
		loadedResources++
		if loadedResources >= totalResources {
			logInfo("All resources have been loaded")
			if callback:
				callback()
		}
	}
	var onLoad = func {
		logDebug("Resource loaded")
		onResourceLoadingComplete()
	}
	var onFontLoad = func {
		for(var i = 0 ; i < numberOfFonts ; i++) {
			logDebug("Resource loaded")
			onResourceLoadingComplete()
		}
		numberOfFonts = 0
	}
	var onFontError = func {
		for(var i = 0 ; i < numberOfFonts ; i++) {
			logError("Failed to load resource")
			onResourceLoadingComplete()
		}
		numberOfFonts = 0
	}
	var style as object?
	foreach path in resources {
		var pp = path
		var slash = String.getLastIndexOfCharacter(pp, '/')
		if slash >= 0:
			pp = String.getEndOfString(pp, slash + 1)
		var dot = String.getLastIndexOfCharacter(pp, '.')
		if dot < 0 {
			onResourceLoadingComplete()
			continue
		}
		var ext = String.getEndOfString(pp, dot + 1)
		pp = String.getSubString(pp, 0, dot)
		if String.equalsIgnoreCase(ext, "png") || String.equalsIgnoreCase(ext, "jpg") || String.equalsIgnoreCase(ext, "gif") {
			logDebug("Start loading resource: `" .. pp .. ": `" .. path .. "'")
			nowait (func async {
				try {
					var imgo = await ImageForBrowserJS.forUrl(path)
					imageCache[pp] = imgo
					onLoad()
				}
				catch e as Exception {
					logError(e.toString())
				}
			})()
		}
		else if String.equalsIgnoreCase(ext, "otf") || String.equalsIgnoreCase(ext, "ttf") || String.equalsIgnoreCase(ext, "woff") {
			logDebug("Start loading resource: `" .. pp .. ": `" .. path .. "'")
			var head = HTMLDOM.getDocumentHead()
			if style == null {
				style = HTMLDOM.createElement("style")
				HTMLDOM.appendChild(head, style)
			}
			HTMLDOM.appendChild(style, HTMLDOM.createTextNode("@font-face { font-family: " .. pp .. "; src: url('" .. path .. "')}"))
			if HTMLDOM.isInternetExplorer() || HTMLDOM.isEdge() {
				numberOfFonts++
				onResourceLoadingComplete()
			}
			else if HTMLDOM.doesBrowserSupportFontLoading() {
				lang "js" (onFontLoad, onFontError) {{{
					numberOfFonts++;
					document.fonts.load("12px " + pp).then(onFontLoad, onFontError);
				}}}
			}
			else {
				logWarning("Browser does not support font loading: `" .. path .. "'")
				onResourceLoadingComplete()
			}
		}
		else {
			logWarning("Unsupported resource file type `" .. ext .. "': `" .. path .. "'")
			onResourceLoadingComplete()
		}
	}
}

func getResourceImage(id as string) override throws Exception as Image
{
	var v = Map.getValue(imageCache, id)
	if not v:
		Error.throw("resourceImageNotFound", id)
	return v
}

func showConfirmDialog(title as string, message as string, okcallback as function<void>?, cancelcallback as function<void>?) override
{
	var result as bool
	lang "js" {{{
		result = confirm(message);
	}}}
	if result {
		if okcallback:
			okcallback()
	}
	else if cancelcallback {
		cancelcallback()
	}
}

func showMessageDialog(title as string, message as string, callback as function<void>? = null) override
{
	lang "js" {{{
		window.alert(message);
	}}}
	if callback:
		callback()
}

func showErrorDialog(message as string, callback as function<void>? = null) override
{
	lang "js" {{{
		window.alert(message);
	}}}
	if callback:
		callback()
}

func getScreenTopMargin override as int:
	return 0

func getScreenWidth override as int
{
	var v as int
	lang "js" {{{
		v = window.screen.availWidth;
	}}}
	return v
}

func getScreenHeight override as int
{
	var v as int
	lang "js" {{{
		v = window.screen.availHeight;
	}}}
	return v
}

func getScreenDensity override as int
{
	if screenDensity < 1 {
		var ppi as int
		// HACK: Firefox (and likely others) stubbornly uses a DPI value of n*96, regardless of what it really is.
		// But in reality, most people wouldn't have 96 DPI anymore .. Thus we'll make a reasonable middle-ground guess
		// of 1.3x the DPI (thus 1.3in, not 1in)
		lang "js" {{{
			var div = document.createElement("div");
			div.setAttribute("style", "width: 1.3in; padding: 0; visibility: hidden; position: fixed; left: 0; top: 0;");
			var bodys = document.getElementsByTagName("body");
			bodys[0].appendChild(div);
			ppi = div.offsetWidth;
			if(ppi < 1) {
				ppi = 1;
			}
			bodys[0].removeChild(div);
		}}}
		var qs = WebBrowserContext.getQueryString()
		if String.isNotEmpty(qs): foreach q in String.split(qs, '&') {
			if String.startsWith(q, "jkopDpi=") == false:
				continue
			var val = String.getEndOfString(q, 8)
			if String.isEmpty(val) == false {
				var nppi = Integer.forStringOrNull(val)
				if nppi != null:
					ppi = nppi
			}
			break
		}
		screenDensity = ppi
	}
	return screenDensity
}

func getHeightValue(spec as string) override as int:
	return Length.asPixels(spec, getScreenDensity())

func getWidthValue(spec as string) override as int:
	return Length.asPixels(spec, getScreenDensity())

func startTimer(timeout as long, callback as function<void>) override as GuiApplicationContext.TimerObject
{
	var timer = new MyTimerObject()
	lang "js" {{{
		timer.setTimerID(setTimeout(callback, timeout));
	}}}
	return timer
}

func executeAsyncDelay(timeout as long) async override throws Exception
{
	lang "js" {{{
		await new Promise(resolve => {
			setTimeout(() => { resolve() }, timeout);
		})
	}}}
}

func getApplicationDataDirectory override stub throws Exception as File?
func enableKeepAwakeMode override stub
func disableKeepAwakeMode override stub
