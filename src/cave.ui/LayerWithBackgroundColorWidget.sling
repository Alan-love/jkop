
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is LayerWidget #widget:

IFNDEF "target_html" {
	var canvas as CanvasWidget
}

ctor(ctx as GuiApplicationContext)
{
	base(ctx)
	IFNDEF "target_html" {
		addWidget(canvas = new CanvasWidget(ctx))
	}
}

var widgetColor as Color
var widgetOutlineColor = Color.black()
var widgetOutlineWidth as int
var widgetBorderStyle as int
var widgetTopLeftRadius as double
var widgetTopRightRadius as double
var widgetBottomRightRadius as double
var widgetBottomLeftRadius as double

func setWidgetColor(color as Color) as this
{
	widgetColor = color
	IFDEF "target_html" {
		if widgetColor {
			HTMLDOM.setStyle(this.element, "background-color", HTMLDOM.colorToRGBA(widgetColor))
		}
		else {
			HTMLDOM.removeStyle(this.element, "background-color")
		}
	}
	ELSE {
		assert canvas
		canvas.setWidgetColor(color)
	}
	return this
}

func getWidgetColor as Color:
	return widgetColor

func setWidgetOutlineColor(color as Color)
{
	widgetOutlineColor = color
	updateCanvas()
}

func getWidgetOutlineColor as Color:
	return widgetOutlineColor

func setWidgetOutlineWidth(width as int #notLiteral)
{
	widgetOutlineWidth = width
	updateCanvas()
}

func getWidgetOutlineWidth as int:
	return widgetOutlineWidth

func updateCanvas private
{
	IFDEF "target_html" {
		if widgetOutlineWidth < 0 {
			HTMLDOM.removeStyle(element, "border")
			HTMLDOM.removeStyle(element, "border-color")
			HTMLDOM.removeStyle(element, "border-style")
		}
		else {
			HTMLDOM.setStyle(element, "border", String.asString(widgetOutlineWidth) .. "px")
			HTMLDOM.setStyle(element, "border-style", "solid")
			HTMLDOM.setStyle(element, "border-color", widgetOutlineColor.toRgbString())
		}
	}
	ELSE {
		ERROR "Not implemented."
	}
}

func setWidgetRoundingRadiusTopLeft(radius as double #notLiteral)
{
	widgetTopLeftRadius = radius
	updateCanvasRoundingRadius()
}

func setWidgetRoundingRadiusTopRight(radius as double #notLiteral)
{
	widgetTopRightRadius = radius
	updateCanvasRoundingRadius()
}

func setWidgetRoundingRadiusBottomLeft(radius as double #notLiteral)
{
	widgetBottomLeftRadius = radius
	updateCanvasRoundingRadius()
}

func setWidgetRoundingRadiusBottomRight(radius as double #notLiteral)
{
	widgetBottomRightRadius = radius
	updateCanvasRoundingRadius()
}

func setWidgetRoundingRadius(radius as double #notLiteral):
	setWidgetRoundingRadius(radius, radius, radius, radius)

func setWidgetRoundingRadius(lradius as double #notLiteral, rradius as double #notLiteral):
	setWidgetRoundingRadius(lradius, rradius, rradius, lradius)

func setWidgetRoundingRadius(tlradius as double #notLiteral, trradius as double #notLiteral, brradius as double #notLiteral, blradius as double #notLiteral)
{
	widgetTopLeftRadius = tlradius
	widgetTopRightRadius = trradius
	widgetBottomRightRadius = brradius
	widgetBottomLeftRadius = blradius
	updateCanvasRoundingRadius()
}

func updateCanvasRoundingRadius private
{
	var isRounded = true
	if widgetTopLeftRadius <= 0.0 && widgetTopRightRadius <= 0.0 && widgetBottomRightRadius <= 0.0 && widgetBottomLeftRadius <= 0.0 {
		isRounded = false
	}
	IFDEF "target_html" {
		if isRounded {
			HTMLDOM.setStyle(element, "border-radius", String.asString(widgetTopLeftRadius) .. "px " ..
				String.asString(widgetTopRightRadius) .. "px " ..
				String.asString(widgetBottomRightRadius) .. "px " ..
				String.asString(widgetBottomLeftRadius) .. "px")
		}
		else {
			HTMLDOM.removeStyle(element, "border-radius")
		}
	}
	ELSE {
		if canvas:
			canvas.setWidgetRoundingRadius(widgetTopLeftRadius, widgetTopRightRadius, widgetBottomRightRadius, widgetBottomLeftRadius)
	}
}

func setWidgetBorderStyle(style as int) as this
{
	widgetBorderStyle = style
	IFDEF "target_html" {
		if style == CanvasWidget.BORDER_STYLE_SOLID {
			HTMLDOM.setStyle(element, "border-style", "solid")
		}
		else if style == CanvasWidget.BORDER_STYLE_DASHED {
			HTMLDOM.setStyle(element, "border-style", "dashed")
		}
		else if style == CanvasWidget.BORDER_STYLE_DOTTED {
			HTMLDOM.setStyle(element, "border-style", "dotted")
		}
		else if style == CanvasWidget.BORDER_STYLE_NONE {
			HTMLDOM.setStyle(element, "border-style", "none")
		}
	}
	ELSE {
		ERROR "Not implemented"
	}
	return this
}
