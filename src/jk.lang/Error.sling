
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is StringObject is StringObjectWithDebug:

func throw(code as string, detail as dynamic? = null, stackTrace as string? = null) static #throws:
	throw ExceptionWithError.forError(new this() {
		code code
		detail detail
		stackTrace stackTrace
	})

func forCode(code as string, detail as dynamic? = null, stackTrace as string? = null) static as this:
	return new this() {
		code code
		detail detail
		stackTrace stackTrace
	}

func isError(o as object?) static as bool
{
	var e = assert o as this
	if String.isEmpty(e.code):
		return false
	return true
}

prop code as string
prop detail as dynamic?
prop stackTrace as string?

func clear as this
{
	code = ""
	detail = null
	stackTrace = null
	return this
}

func toStringWithDefault(debug as bool, defaultError as string) as string
{
	var details = ""
	if detail {
		if detail is StringObjectWithDebug {
			details = (detail as! StringObjectWithDebug).toStringWithDebug(debug)
		}
		else {
			var vv = String.asValueOrNull(detail)
			if vv:
				details = vv
		}
		if String.isEmpty(details) && detail is object:
			details = String.forObjectInformation(detail as! object)
	}
	var sb = new StringBuilder()
	if String.isNotEmpty(code) {
		sb.appendString(code)
	}
	else {
		sb.appendString(defaultError)
	}
	var stackTrace = this.stackTrace
	if String.isNotEmpty(details) || String.isNotEmpty(stackTrace) {
		sb.appendCharacter(':')
		if String.isNotEmpty(details):
			sb.appendString(details)
		if String.isNotEmpty(stackTrace) {
			sb.appendCharacter(':')
			sb.appendString(stackTrace)
		}
	}
	return sb.toString()
}

func toStringWithDebug(debug as bool) as string:
	return toStringWithDefault(debug, "unknownError")

func toString as string:
	return toStringWithDebug(false)
