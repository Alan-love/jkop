
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class:

func createClassInstanceByName(qualifiedName as string) static throws Exception as object
{
	IFDEF "target_sushi" {
		var code = " return _g." .. qualifiedName .. "._construct0(_g." .. qualifiedName .. "._create())"
		var ff = assert lang "lua" as function<dynamic> {{{ _vm:parse_to_function(code) }}}
		return ff()
	}
	ELSE IFDEF "target_node_js" {
		var lin = String.getLastIndexOfCharacter(qualifiedName, '.')
		var length = String.getLength(qualifiedName)
		var className = String.getSubString(qualifiedName, lin, length - lin)
		var module = String.getSubString(qualifiedName, 0, lin)
		var ff as object?
		lang "js" (className, module) {{{
			const ev = "require(\'./" + module + "\')" + className + ".NEW()";
			ff = eval(ev);
		}}}
		if not ff:
			Error.throw("failedToCreateClassInstance", qualifiedName)
		return ff
	}
	ELSE IFDEF "target_browser_js" {
		nop qualifiedName
		ERROR NOT_SUPPORTED
	}
	ELSE {
		nop qualifiedName
		ERROR NOT_IMPLEMENTED
	}
}

func createClassInstanceByTypeinfo(typeinfo as typeinfo) static throws Exception as object
{
	IFDEF "target_sushi" {
		var typename = lang "lua" (@typeinfo) as string {{{ typeinfo }}}
		assert String.startsWith(typename, "class:")
		typename = String.getEndOfString(typename, 6)
		return createClassInstanceByName(typename)
	}
	ELSE IFDEF "target_node_js" {
		var ff as object?
		lang "js" (@typeinfo) {{{
			ff = typeinfo.NEW();
		}}}
		if not ff:
			Error.throw("failedToCreateClassInstance", @typeinfo)
		return ff
	}
	ELSE IFDEF "target_browser_js" {
		return lang "js" (@typeinfo) as object {{{ typeinfo.NEW() }}}
	}
	ELSE IFDEF "target_swift" {
		lang "swift" {{{
			if let otyp = typeinfo as? NSObject.Type {
				return otyp.init()
			}
		}}}
		Error.throw("unableToCreateClassInstanceForType", @typeinfo)
	}
	ELSE {
		nop @typeinfo
		ERROR NOT_IMPLEMENTED
	}
}

func callMethod(objectInstance as dynamic, methodName as string, arguments as array<dynamic>) static as dynamic?
{
	IFDEF "target_sushi" {
		var sb = new StringBuilder()
		sb.appendString("return function(object, arguments) return object:")
		sb.appendString(methodName)
		sb.appendString("(")
		var sz = sizeof arguments
		for(var n = 0 ; n < sz ; n++) {
			if n > 0:
				sb.appendString(",")
			sb.appendString("arguments[")
			sb.appendInteger(n + 1)
			sb.appendString("]")
		}
		sb.appendString(") end")
		var code = sb.toString()
		var ff = assert lang "lua" as function<function<(dynamic,array<dynamic>) as dynamic?>> {{{ _vm:parse_to_function(code) }}}
		var ffx = assert ff()
		return ffx(objectInstance, arguments)
	}
	ELSE IFDEF "target_js" {
		return lang "js" (objectInstance, methodName, arguments) as dynamic? {{{ objectInstance[methodName](arguments) }}}
	}
	ELSE {
		nop objectInstance
		nop methodName
		nop arguments
		ERROR NOT_IMPLEMENTED
	}
}

func getDataTypeInfo(variable as dynamic) static as typeinfo:
	return typeof variable

func isSameType(primary as dynamic?, other as dynamic?) static as bool
{
	if primary == null && other == null:
		return true
	if primary == null || other == null:
		return false
	var ta = typeof primary
	var tb = typeof other
	if ta as! object == tb as! object:
		return true
	return false
}

func isInstanceOf(objectInstance as dynamic, typeinfo as typeinfo) static as bool
{
	IFDEF "target_dotnet_cs" {
		lang "cs" {{{
			if(typeinfo == null) {
				return false;
			}
		}}}
		return lang "cs" (objectInstance, @typeinfo) as bool {{{ typeinfo.IsAssignableFrom(objectInstance.GetType()) }}}
	}
	ELSE IFDEF "target_sushi" {
		return lang "lua" (objectInstance, @typeinfo) as bool {{{ _vm:is_instance_of(objectInstance, typeinfo) }}}
	}
	ELSE IFDEF "target_js" {
		var tt = @typeinfo
		lang "js" (objectInstance, tt) {{{
			if(typeof tt.IS_INSTANCE === 'function') {
				return tt.IS_INSTANCE(objectInstance);
			}
			if(typeof tt === 'function') {
				return objectInstance instanceof tt;
			}
		}}}
		return false
	}
	ELSE IFDEF "target_swift" {
		var oo = objectInstance as object
		if not oo:
			return false
		var oti = lang "swift" (@typeinfo) as typeinfo? {{{ typeinfo as? AnyClass }}}
		if not oti:
			return false
		return lang "swift" (oo, oti) as bool {{{ oo!.isKind(of: oti!) }}}
	}
	ELSE {
		nop objectInstance
		nop @typeinfo
		ERROR NOT_IMPLEMENTED
	}
}

func getCodeFilePathForObjectOrNull(oo as object) static as string?
{
	IFDEF "target_dotnet_cs" {
		var path as string?
		lang "cs" (oo) {{{
			var type = oo.GetType();
			if(type != null) {
				var assembly = type.Assembly;
				if(assembly != null) {
					path = assembly.Location;
				}
			}
		}}}
		return path
	}
	ELSE {
		nop oo
		return null
	}
}
