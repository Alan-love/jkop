
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class:

class MyIntegerObject is IntegerObject private
{
	prop value as int

	func toInteger implement as int:
		return value
}

func wrapValue(value as int) static as IntegerObject:
	return new MyIntegerObject() {
		value value
	}

func asValueOrDefaultValue(obj as dynamic?, defaultValue as int = 0) static as int
{
	var v = asObject(obj)
	if not v:
		return defaultValue
	return v.toInteger()
}

func asValueOrThrowError(obj as dynamic?) static throws Exception as int
{
	var v = asObject(obj)
	if not v:
		Error.throw("valueCannotBeConvertedToInteger", obj)
	return v.toInteger()
}

func asObject(obj as dynamic?) static as IntegerObject?
{
	if not obj:
		return null
	if obj is int:
		return wrapValue(obj as! int)
	if obj is IntegerObject {
		var oi = obj as! IntegerObject
		return wrapValue(oi.toInteger())
	}
	if obj is long {
		var v = obj as! long
		return wrapValue(v as! int)
	}
	if obj is LongIntegerObject {
		var oi = obj as! LongIntegerObject
		return wrapValue(oi.toLongInteger() as! int)
	}
	if obj is string {
		var ii = forStringOrNull(obj as! string)
		if ii == null:
			return null
		return wrapValue(ii)
	}
	if obj is StringObject {
		var ii = forStringOrNull((obj as! StringObject).toString())
		if ii == null:
			return null
		return wrapValue(ii)
	}
	if obj is double {
		var v = obj as! double
		var vv = v as! int
		return wrapValue(vv)
	}
	if obj is DoubleObject {
		var od = obj as! DoubleObject
		var vv = od.toDouble() as! int
		return wrapValue(vv)
	}
	if obj is bool {
		if obj as! bool == true:
			return wrapValue(1)
		return wrapValue(0)
	}
	if obj is BooleanObject {
		if (obj as! BooleanObject).toBoolean():
			return wrapValue(1)
		return wrapValue(0)
	}
	if obj is char {
		var v = obj as! char
		return wrapValue(v as! int)
	}
	if obj is CharacterObject {
		var oc = obj as! CharacterObject
		return wrapValue(oc.toCharacter() as! int)
	}
	return null
}

func forStringOrNull(str as string) static as int?
{
	description [[
		Converts the string to an integer. If the string does not represent a valid
		integer, than a null value is returned. Eg. forStringOrNull("99") == 99 Eg.
		forStringOrNull("asdf") == null
	]]
	if String.isEmpty(str):
		return null
	var v = 0
	var first = true
	var negative = false
	foreach c in String.iterate(str) {
		if first && c == '-' {
			negative = true
			first = false
			continue
		}
		if c >= '0' && c <= '9' {
			v = v * 10
			v += (c - '0') as! int
		}
		else {
			return null
		}
		first = false
	}
	if negative:
		v *= -1
	return v
}

func forStringOrDefaultValue(str as string, defaultValue as int = 0) static as int
{
	description [[
		Converts the string to an integer. If the string does not represent a valid
		integer, then the defaultValue is returned. Eg. forStringOrDefaultValue("99",
		10) == 99 Eg. forStringOrDefaultValue("asdf", 10) == 10
	]]
	var v = forStringOrNull(str)
	if v == null:
		return defaultValue
	return v
}

func forStringOrThrowError(str as string) static throws Exception as int
{
	description [[
		Converts the string to an integer. If the string does not represent a valid
		integer, then an exception is thrown. Eg. forStringOrThrowError("99") = 99 Eg.
		forStringOrThrowError("asdf") throws exception
	]]
	var v = forStringOrNull(str)
	if v == null:
		Error.throw("stringCannotBeConvertedToInteger", str)
	return v
}

func forHexStringOrDefaultValue(str as string, defaultValue as int = 0) static as int
{
	description [[
		Converts the string to an integer, while treating the string as a hexadecimal
		representation of a number, eg. forHexStringOrDefaultValue("a") = 10
	]]
	if String.isEmpty(str):
		return defaultValue
	var v = 0
	foreach c in String.iterate(str) {
		if c >= '0' && c <= '9' {
			v = v * 16
			v += (c - '0') as! int
		}
		else if c >= 'a' && c <= 'f' {
			v = v * 16
			v += (10 + c - 'a') as! int
		}
		else if c >= 'A' && c <= 'F' {
			v = v * 16
			v += (10 + c - 'A') as! int
		}
		else {
			return defaultValue
		}
	}
	return v
}

func forOctalStringOrDefaultValue(str as string, defaultValue as int = 0) static as int
{
	description [[
		Converts the string to an integer, while treating the string as an octal
		representation of a number.
	]]
	if String.isEmpty(str):
		return defaultValue
	var v = 0
	foreach c in String.iterate(str) {
		if c >= '0' && c <= '7' {
			v = v * 8
			v += (c - '0') as! int
		}
		else {
			return defaultValue
		}
	}
	return v
}

func forBinaryStringOrDefaultValue(str as string, defaultValue as int = 0) static as int
{
	description [[
		Converts the string to an integer, while treating the string as a binary
		representation of a number.
	]]
	if String.isEmpty(str):
		return defaultValue
	var v = 0
	foreach c in String.iterate(str) {
		if c >= '0' && c <= '1' {
			v = v * 2
			v += (c - '0') as! int
		}
		else {
			return defaultValue
		}
	}
	return v
}
