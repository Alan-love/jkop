
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.jco.common

class is JcoComponent #jco #component:

prop horizontal as string #content = "center"
prop vertical as string #content = "center"

IFDEF "target_browser_js"
{
	func getPlaceItemsValue(value as string) private as string
	{
		if value == "center":
			return value
		if value == "left" || value == "top":
			return "start"
		if value == "right" || value == "bottom":
			return "end"
		return "center"
	}

	func addCssRules(sheet as CSSBuilder) override throws Exception
	{
		base.addCssRules(sheet)
		sheet.define(":host", new map<string,string> {
			display "grid"
			"place-items" getPlaceItemsValue(vertical) .. " " .. getPlaceItemsValue(horizontal)
			width "100%"
			height "100%"
		})
	}
}
ELSE IFDEF "target_ios_swift"
{
	import [magical:UIKit]

	func layoutSubviews override #magical
	{
		lang "swift" {{{
			let csize = self.bounds.size
			let rsz = CGSizeMake(csize.width, csize.height)
			for subview in subviews {
				let sframe = subview.sizeThatFits(rsz)
				var x = 0.0
				var y = 0.0
				var w = sframe.width
				var h = sframe.height
				if w > csize.width {
					w = csize.width
				}
				if h > csize.height {
					h = csize.height
				}
				if horizontal == "left" {
					x = 0
				}
				else if horizontal == "right" {
					x = csize.width - w
				}
				else {
					x = (csize.width - w) / 2
				}
				if vertical == "top" {
					y = 0
				}
				else if vertical == "bottom" {
					y = csize.height - h
				}
				else {
					y = (csize.height - h) / 2
				}
				subview.frame = CGRectMake(x, y, w, h)
			}
		}}}
	}
}

compose
{
	{
		composeChildren(context)
	}
}
