
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.jco.common

class is LayerWidget #jco #component:

prop clickHandler as function<() throws Exception>?
prop pointerEnterHandler as function<() throws Exception>?
prop pointerLeaveHandler as function<() throws Exception>?

func onClicked virtual throws Exception
{
	var ch = clickHandler
	if ch:
		ch()
}

func onPointerEnter virtual throws Exception
{
	var ch = pointerEnterHandler
	if ch:
		ch()
}

func onPointerLeave virtual throws Exception
{
	var ch = pointerLeaveHandler
	if ch:
		ch()
}

section private
{
	func handleOnClicked:
		executeAndHandleExceptions(func throws Exception => onClicked())

	func handleOnPointerEnter:
		executeAndHandleExceptions(func throws Exception => onPointerEnter())

	func handleOnPointerLeave:
		executeAndHandleExceptions(func throws Exception => onPointerLeave())
}

IFDEF "target_browser_js"
{
	func addCssRules(sheet as CSSBuilder) override throws Exception
	{
		base.addCssRules(sheet)
		sheet.define(":host", new map<string,string> {
			cursor "pointer"
			"user-select" "none"
		})
	}

	func onMounted override throws Exception
	{
		base.onMounted()
		var element = asElement()
		element.setEventCapturer("click", func(event as dynamic) => handleOnClicked())
		element.setEventObserver("mouseenter", func(event as dynamic) => handleOnPointerEnter())
		element.setEventObserver("mouseleave", func(event as dynamic) => handleOnPointerLeave())
	}

	func onUnmounted override throws Exception
	{
		base.onUnmounted()
		var element = asElement()
		element.setEventCapturer("click", null)
		element.setEventObserver("mouseenter", null)
		element.setEventObserver("mouseleave", null)
	}
}

compose
{
	{
		composeChildren(context)
	}
}
