
/*
 * This file is part of Eqela Jkop
 * Copyright (c) 2016-2024 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.jco.common

class is JcoComponent #jco #component:

class TabWidget is JcoComponent public #jco #component
{
	prop title as string = "" #content

	compose
	{
		{
			composeChildren(context)
		}
	}
}

var selectedTab as object?

func selectTab(tab as object)
{
	selectedTab = tab
	update()
}

compose
{
	{
		if selectedTab == null:
			selectedTab = Vector.getValue(children, 0)
	}
	<VerticalBoxWidget>
		<HorizontalBoxWidget>
			{
				foreach child as TabWidget in children:
					compose {
						<WeightWidget weight="1">
							<PointerCaptureLayerWidget clickHandler=@{func throws Exception => selectTab(child)}>
								<TabHeaderWidget title=@{child.title} selected=@{child == selectedTab ? "true" : "false"} />
							</PointerCaptureLayerWidget>
						</WeightWidget>
					}
			}
		</HorizontalBoxWidget>
		<WeightWidget weight="1">
			{
				if var selectedTab = selectedTab:
					context.addObject(selectedTab)
			}
		</WeightWidget>
	</VerticalBoxWidget>
}
